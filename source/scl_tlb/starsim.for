!+
!      STARSIM.HLP - User documentation for DSCL procedure STARSIM.
!                    Also contains implemantation information.
!
!      (Part of the STARSIM suite).
!
!
!                          +-----------+
!                          |           |
!                          |  STARSIM. |
!                          |           |
!                          +-----------+
!
!:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
!
!
!  Purpose:
!     Generate an image frame containing an artificial star field
!   with user defined parameters.
!
!  General Description:
!     STARSIM is a DSCL procedure for generating an image frame
!   consisting of an artificial star field. The stars included
!   in the field have known photometric properties and hence
!   the frame can be used for testing 2D photometry routines.
!   A star field consisting of a background field on which a
!   globular cluster may be optionally superimposed is generated.
!   Separate luminosity functions are used to generate the
!   apparent magnitudes of the field and cluster stars. All the
!   stars have Moffat scattering profiles. Poisson noise is
!   included in the generated frame so that an electronic type
!   detector, such as a CCD, is simulated. The star field is
!   assumed to be viewed through a Johnson B broadband filter
!   and the effects of interstellar absorption and atmospheric
!   extinction are included.
!     STARSIM is more completely described, including the principles
!   that go into the simulation, in SUN41, which should
!   be consulted for further details.
!
!  Usage:
!     STARSIM may be used with either its default luminosity functions
!   or with user supplied ones. If the defaults are to be used then
!   simply type
!
!                          STARSIM
!
!   If user supplied luminosity functions are to be used then type
!
!                 STARSIM <lumfile> <globfile>
!
!   where <lumfile> and <globfile> are the names of the field
!   luminosity function and the globular cluster luminosity function
!   files respectively. It should be noted that because these
!   parameters will be passed to a batch program their directory
!   structure should be specified in full. SUN41 explains  how the
!   user can construct his own luminosity functions.
!     The mandatory prompts now start. firstly the user is prompted for
!   the directory where the star field and target star files are to be
!   created. This directory must be specified in full because it is to
!   be passed to a batch job. A quite large amount of space, up to
!   several thousand blocks, may be required.
!     The name of the control parameter file is prompted for next. Here
!   it is sufficient to give the file name because it will automatically
!   be placed in the subdirectory selected above.
!   The user is prompted for the values defining the field to be
!   simulated and these values are written to the control parameter
!   file. This prompting is handled by program SIMPAR and the
!   on-line help information for SIMPAR should be consulted for
!   details of the parameters prompted for.
!     The names required for the image frame generated and a
!   "star catalogue" file are prompted for next. Again it is
!   sufficient to give the required filename and they will be placed in
!   the chosen directory. The "star catalogue" file
!   is a Starlink bulk data frame which contains the positions and
!   apparent magnitudes of all the stars in the simulated image
!   frame.
!     A batch job to produce the image frame is then submitted. In
!   addition to the image frame and star catalogue files two pieces
!   of hard copy output are produced; a listing containing details
!   of the frame generated and a "finding chart" for the brighter
!   stars in the field.
!     The listing starts with any error or warning messages that
!   may have been generated by the program and then lists all
!   parameters used to generate the field. Next follows a listing
!   containing entries for all the stars in the field. If a
!   globular cluster is included in the field the cluster and
!   field stars are differentiated by subheadings. The parameters
!   listed for each star are;
!
! 1)  Running count.
!
! 2)  X position (pixels).
!
! 3)  Y position (pixels).
!
! 4)  True apparent magnitude as generated from the luminosity
!     function.
!
! 5)  The number of detected photons corresponding to the apparent
!     magnitude 4).
!
! 6)  The number of detected photons after allowing for the truncation
!     caused by the thresholded profile.
!
! 7)  The apparent magnitude applied to the image corresponding to the
!     thresholded luminosity 6). For very faint stars which are nowhere
!     brighter than the luminosity threshold the magnitude is replaced
!     by a message saying "Not applied to image."
!
! 8)  The final column contains a row of 4 hash characters ("#") if the star
!     is brighter than the plotting threshold (and hence appears on the
!     "finding chart", below), otherwise it is blank.
!
!     A "finding chart" is automatically produced on the Versatec. It
!   indicates the positions of all stars brighter than the plotting
!   threshold. The stars are indicated by circles; the diameter being
!   proportional to magnitude.
!
!
!::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
!
!
!  Implementation details:
!  **********************
!
!     The following details of file STARSIM.SCL may need to be changed
!   for implementation at a given Starlink node.
!
!   (i) Line 5700. The command "VPLOT" may need to be changed if the
!       local command for sending Fings plot files to the Versatec
!       is different.
!
!   (ii) Line 6300. The procedure is supplied so that a batch
!       job is submitted to the queue SYS$BATCH which should be
!       available on all nodes. However the batch job is quite
!       short and the /QUEUE qualifier may be modified so that the
!       job is submitted to any queue for fast jobs
!       which the node may have (eg. SHORT at ROE, FASTBATCH at RGO).
!
!
!  A C Davenhall./ROE/                                      23/1/83.
!  A C Davenhall./ROE/ {Modified}                           22/4/83.
!- ;;
!
!  Setup defaults for the luminosity functions.
!
$ IF P1 .EQS. "" THEN P1 := "ASPDIR:LUMFUN.BDF"
$ IF P2 .EQS. "" THEN P2 := "ASPDIR:GLOBFUN.BDF"
$ WRT  :==  WRITE SYS$OUTPUT
!
!  Inquire the work directory where the various files are to be
!  written.
!
$GET_DIREC:
$  INQUIRE WDIREC "Enter directory to hold output files"
$  IF WDIREC .EQS. "" THEN GOTO GET_DIREC
!
!  Run the program to obtain values for the parameter file.
!
$GET_CONTROL:
$  INQUIRE CONTROL "Enter name of control parameter file"
$  IF CONTROL .EQS. "" THEN GOTO GET_CONTROL
LET SIMPAR_PARAM = 'WDIREC''CONTROL'
SIMPAR
!
!  Prompt for the names of the star and catalogue files.
!
$GET_STAR:
$  INQUIRE STAR "Enter file name for starfield"
$  IF STAR .EQS. "" THEN GOTO GET_STAR
$GET_CATAL:
$  INQUIRE CATAL "Enter file name for star catalogue"
$  IF CATAL .EQS. "" THEN GOTO GET_CATAL
!
!  Generate batch job file.
!
$ OPEN /WRITE BATCH_FILE BSTARSIM.COM
$WRITE BATCH_FILE   "$ @SSC:LOGIN.COM"
$WRITE BATCH_FILE   "SET VERIFY"
$WRITE BATCH_FILE   "SET DEF ''WDIREC'"
$WRITE BATCH_FILE   "RUNSTAR ASPDIR:STARSIMP"
$WRITE BATCH_FILE    CONTROL
$WRITE BATCH_FILE    P1
$WRITE BATCH_FILE    P2
$WRITE BATCH_FILE    STAR
$WRITE BATCH_FILE   "SIMULATED STAR FIELD."
$WRITE BATCH_FILE    CATAL
$WRITE BATCH_FILE   "STAR CATALOGUE."
$WRITE BATCH_FILE   "$ VPLOT"
$WRITE BATCH_FILE   "$ EXIT"
$CLOSE BATCH_FILE
!
!  Submit the batch job.
!
$SUBMIT BSTARSIM.COM /NOTIFY /DELETE
!
$WRT "      "
