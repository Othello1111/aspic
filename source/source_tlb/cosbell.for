	PROGRAM COSBELL
C
C+	PROGRAM COSBELL
C
C	THIS PROGRAM ACCEPTS A 1D OR 2D IMAGE AND:-
C		OPTIONALLY ZERO-MEANS THE DATA
C		APPLIES A COSINE BELL FUNCTION
C
C	PARAMETERS ARE:
C		INPUT	REAL INPUT FRAME
C		ZMEAN	LOGICAL TRUE IFF REQUIRE ZERO-MEANING
C                       (DEFAULTED TO TRUE IN CONNECTION FILE)
C		FRAC	PROPORTION OF EACH END OF THE ARRAY
C                       (DEFAULTED TO 0.0625 IN CONNECTION FILE)
C			WHICH IS USED FOR COSINE-BELLING
C		OUTPUT	REAL OUTPUT FRAME
C
C	WFL RGO FEB 1981
C
C-
	IMPLICIT INTEGER (A-Z)
      INCLUDE 'INTERIM(ERRPAR)'
      INCLUDE 'INTERIM(FMTPAR)'
	INTEGER AXIS(2)
	REAL FRAC
	LOGICAL ZMEAN
C
C	GET INPUT FRAME
C
	AXIS(2)=1
10	CALL RDIMAG('INPUT',FMT_R,2,AXIS,NAXIS,INPTR,STATUS)
	IF (STATUS.NE.ERR_NORMAL) THEN
		CALL CNPAR('INPUT',STATUS)
		GOTO 10
	ENDIF
	IF (NAXIS.GT.2) THEN
		CALL WRUSER('FRAME MUST BE 1 OR 2D',STATUS)
		CALL CNPAR('INPUT',STATUS)
		GOTO 10
	ENDIF
C
C	GET OUTPUT FRAME
C
20	CALL WRIMAG('OUTPUT',FMT_R,AXIS,NAXIS,OUTPTR,STATUS)
	IF (STATUS.NE.ERR_NORMAL) THEN
		CALL CNPAR('OUTPUT',STATUS)
		GOTO 20
	ENDIF
C
C	DECIDE WHETHER DATA IS TO BE ZERO-MEANED
C
30	CALL RDKEYL('ZMEAN',.FALSE.,1,ZMEAN,I,STATUS)
	IF (STATUS.NE.ERR_NORMAL) THEN
		CALL CNPAR('ZMEAN',STATUS)
		GOTO 30
	ENDIF
C
C	AND DETERMINE THE FRACTION AT EACH END OF THE DATA FOR
C	COSINE BELLING
C
40	CALL RDKEYR('FRAC',.FALSE.,1,FRAC,I,STATUS)
	IF (STATUS.NE.ERR_NORMAL) THEN
		CALL CNPAR('FRAC',STATUS)
		GOTO 40
	ENDIF
	IF (FRAC.LE.0.0.OR.FRAC.GT.0.25) THEN
		CALL WRUSER('MUST BE > 0 AND < 1/4',STATUS)
		CALL CNPAR('FRAC',STATUS)
		GOTO 40
	ENDIF
C
C	ROUTINE COSBELL2 DOES THE WORK
C
	CALL COSBELL2(%VAL(INPTR),AXIS(1),AXIS(2),ZMEAN,FRAC,%VAL(OUTPTR))
C
C	FREE DATA AND STOP
C
	CALL FRDATA(' ',STATUS)
	CALL EXIT
	END
C
	SUBROUTINE COSBELL2(INPUT,ISIZ1,ISIZ2,ZMEAN,FRAC,OUTPUT)
C
C	ROUTINE TO ZERO-MEAN A REAL INPUT ARRAY (OPTIONAL) THEN
C	COSINE BELL IT USING A FRACTION FRAC (LE 0.25) AT EACH
C	END. THE INPUT ARRAY MAY HAVE ISIZ2=1, WHICH INDICATES
C	THAT IT IS 1D
C
C	ARGUMENTS
C		INPUT	REAL INPUT ARRAY
C		ISIZ1/2	DIMENSIONS OF INPUT
C		ZMEAN	LOGICAL TRUE IFF REQUIRE ZERO MEAN
C		FRAC	FRACTION OF EACH END OF INPUT TO USE
C			FOR COSINE BELLING
C		OUTPUT	REAL OUTPUT ARRAY
C
C	WFL RGO FEB 1981
C
	REAL INPUT(0:ISIZ1-1,0:ISIZ2-1),OUTPUT(0:ISIZ1-1,0:ISIZ2-1),FRAC,VAL
	LOGICAL ZMEAN
	PARAMETER (PI=3.141592654)
	ISIZ1M1=ISIZ1-1
	ISIZ2M1=ISIZ2-1
C
C	IF ZMEAN IS TRUE, CALCULATE THE MEAN IN VAL (WHICH IS
C	ZERO OTHERWISE)
C
	IF (ZMEAN) THEN
		CALL MEANVAL2(INPUT,ISIZ1*ISIZ2,VAL)
	ELSE
		VAL=0.0
	ENDIF
C
C	NOW APPLY THE COSINE BELL
C
	R1=PI/FRAC/ISIZ1
	OUTPUT(0,0)=0.0
	IF (ISIZ2.EQ.1) THEN
		DO I=1,ISIZ1M1
			IF (I.LT.ISIZ1*FRAC) THEN
				RAT1=(1.0-COS(R1*I))/2.0
			ELSE IF (I.LE.ISIZ1*(1.0-FRAC)) THEN
				RAT1=1.0
			ELSE
				RAT1=(1.0-COS(R1*(ISIZ1-I)))/1.0
			ENDIF
			OUTPUT(I,0)=RAT1*(INPUT(I,0)-VAL)
		ENDDO
	ELSE
		R2=PI/FRAC/ISIZ2
		DO I=1,ISIZ1
			IF (I.LE.ISIZ1*FRAC) THEN
				OUTPUT(I,0)=(1.0-COS(R1*I))/2.0
			ELSE
				OUTPUT(I,0)=0.0
			ENDIF
		ENDDO
		DO I=1,ISIZ2M1
			IF (I.LE.ISIZ2*FRAC) THEN
				RAT2=(1.0-COS(R2*I))/2.0
				OUTPUT(0,I)=RAT2
			ELSE IF (I.LE.ISIZ2*(1.0-FRAC)) THEN
				RAT2=1.0
				OUTPUT(0,I)=0.0
			ELSE
				RAT2=OUTPUT(0,ISIZ2-I)
				OUTPUT(0,I)=0.0
			ENDIF
			DO J=1,ISIZ1M1
				IF (J.LE.ISIZ1*FRAC) THEN
					RAT1=OUTPUT(J,0)
				ELSE IF (J.LE.ISIZ1*(1.0-FRAC)) THEN
					RAT1=1.0
				ELSE
					RAT1=OUTPUT(ISIZ1-J,0)
				ENDIF
				OUTPUT(J,I)=RAT1*RAT2*(INPUT(J,I)-VAL)
			ENDDO
		ENDDO
		DO I=1,ISIZ1M1*FRAC
			OUTPUT(I,0)=0.0
		ENDDO
		DO I=1,ISIZ2M1*FRAC
			OUTPUT(0,I)=0.0
		ENDDO
	ENDIF
	RETURN
	END
