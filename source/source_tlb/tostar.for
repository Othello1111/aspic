	PROGRAM TOSTAR
C
C++	PSEUDO STARLINK ENVIRONMENT PROGRAM ****TOSTAR****
C
C	VERSION#1
C
C	WRITTEN BY W F LUPTON RGO NOVEMBER 1980
C
C	CONVERTS A FILE THAT HAS BEEN CREATED BY TAPEREAD
C	OR RGODR INTO STARLINK BULK DATA FORMAT (ANY DIMENSION)
C	ALL INFORMATION IN THE INPUT HEADER FILE IS PRESERVED AND IS
C	WRITTEN TO THE OUTPUT FRAME AS DESCRIPTORS OF NAME 'COMMENT'
C
C	PARAMETERS:-
C	INPUT	NAME OF FILE CONTAINING INPUT DATA (DOES NOT NEED A
C		FILETYPE AS THE FILES USED ARE KNOWN TO BE OF TYPE
C		.HED AND .DAT)
C	OUTPUT	NAME OF OUTPUT FRAME
C
	IMPLICIT INTEGER(A-Z)
      INCLUDE 'INTERIM(ERRPAR)'
      INCLUDE 'INTERIM(FMTPAR)'
	CHARACTER INPUT*30,WORK*30,COMMENT*72
	INTEGER AXIS(2),SAXIS(2)
	DATA COMMENT/' '/
C
C	PROMPT USER FOR NEXT INPUT FILENAME (STOP IF NULL)
C
10	CALL RDKEYC('INPUT',.FALSE.,1,INPUT,I,STATUS)
	IF (STATUS.EQ.ERR_PARNUL) THEN
		GOTO 9999
	ELSE IF (STATUS.NE.ERR_NORMAL) THEN
		CALL WRERR('INPNAME')
		GOTO 50
	ENDIF
C
C	REMOVE FILETYPE ETC (IF PRESENT) FROM 'INPUT'
C
	I=INDEX(INPUT(1:29),']')
	WORK=INPUT(I+1:)
	J=INDEX(WORK(1:29-I)//'.','.')
	INPUT(I+J:)=' '
C
C	OPEN THE HEADER FILE AND READ THE FIRST RECORD IN
C	ORDER TO OBTAIN THE DIMENSIONS OF THE FRAME
C
	OPEN (UNIT=98,FILE=INPUT//'.HED',FORM='FORMATTED',STATUS='OLD',
     +	IOSTAT=STATUS)
	IF (STATUS.NE.ERR_NORMAL) THEN
		CALL WRERR('OPENHED')
		GOTO 50
	ENDIF
	READ (UNIT=98,FMT=*,IOSTAT=STATUS) NDIM,AXIS
	IF (STATUS.NE.ERR_NORMAL) THEN
		CALL WRERR('READDIM')
		GOTO 40
	ENDIF
	IF (NDIM.EQ.1.AND.AXIS(2).GT.1) THEN
		READ (UNIT=98,FMT='(1X,A40,30X,I9)',IOSTAT=STATUS)
     +		COMMENT(1:40),NLAST
		IF (STATUS.NE.ERR_NORMAL) THEN
			CALL WRERR('READDIM')
			NLAST=AXIS(1)
		ENDIF
		SAXIS(1)=AXIS(1)*(AXIS(2)-1)+NLAST
	ELSE
		SAXIS(1)=AXIS(1)
		SAXIS(2)=AXIS(2)
		NLAST=AXIS(1)
	ENDIF
C
C	ALLOCATE FRAME FILE
C
	CALL WRIMAG('OUTPUT',FMT_SW,SAXIS,NDIM,OUTPUT,STATUS)
	IF (STATUS.NE.ERR_NORMAL) THEN
		CALL WRERR('ALLFRAM')
		GOTO 30
	ENDIF
C
C	AND READ DATA FROM INPUT FILE TO FRAME
C
	CALL READ(%VAL(OUTPUT),AXIS(1),AXIS(2),NLAST,INPUT,STATUS)
	IF (STATUS.NE.ERR_NORMAL) THEN
		GOTO 30
	ENDIF
C
C	NOW LOOP READING COMMENTS AND ADDING THEM AS DESCRIPTORS
C
	IF (COMMENT.NE.' ') THEN
		CALL ADDSCR('OUTPUT','COMMENT',COMMENT,1,STATUS)
	ENDIF
20	READ (UNIT=98,FMT='(A)',IOSTAT=STATUS,END=9999) COMMENT
	IF (STATUS.NE.ERR_NORMAL) THEN
		CALL WRERR('READCOM')
		GOTO 30
	ENDIF
	CALL ADDSCR('OUTPUT','COMMENT',COMMENT,1,STATUS)
	GOTO 20
C
C	NOW RELEASE BULK DATA SPACE, CANCEL KEYS, CLOSE HEADER
C	FILE AND RETURN FOR MORE DATA
C
30	CALL FRDATA('OUTPUT',STATUS)
	CALL CNPAR('OUTPUT',STATUS)
	CALL CNPAR('OUTPUT',STATUS)
40	CLOSE (UNIT=98)
50	CALL CNPAR('INPUT',STATUS)
	GOTO 10
C
C	STOP
C
9999	CALL EXIT
	END
C
	SUBROUTINE READ(ICORE1,ICHAN,IXSEC,NLAST,FILE,STATUS)
C
C	ROUTINE TO READ DATA FRAME INTO BULK DATA ARRAY
C
C	W F LUPTON OCT 1980
C
	IMPLICIT INTEGER(A-Z)
	INTEGER*2 ICORE1(ICHAN,IXSEC)
	CHARACTER*(*) FILE
C
C	OPEN DATA FILE
C
	OPEN (UNIT=97,FILE=FILE//'.DAT',FORM='UNFORMATTED',
     +	STATUS='OLD',IOSTAT=STATUS)
	IF (STATUS.NE.ERR_NORMAL) THEN
		CALL WRERR('OPENDAT')
		GOTO 9999
	ENDIF
	LCHAN=ICHAN
	DO I=1,IXSEC
	IF (I.EQ.IXSEC) THEN
		LCHAN=NLAST
	ENDIF
		READ (UNIT=97,IOSTAT=STATUS) (ICORE1(L,I),L=1,LCHAN)
		IF (STATUS.NE.ERR_NORMAL) THEN
			CALL WRERR('READDAT')
			GOTO 10
		ENDIF
	ENDDO
C
C	CLOSE FILE AND RETURN
C
10	CLOSE (UNIT=97)
9999	RETURN
	END
