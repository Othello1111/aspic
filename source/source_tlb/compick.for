C++
C
C      STARLINK ENVIRONMENT PROGRAM   *** COMPICK ***
C
C      WRITTEN BY K F HARTLEY AT RGO ON 16/8/82
C
C      VERSION #2
C
C      THIS PROGRAM PRODUCES A MORE COMPACT VERSION OF AN ARRAY ,
C      FOR EXAMPLE SO THAT IT WILL FIT ON AN ARGS DISPLAY SCREEN
C
C      IT WORKS BY PICKING EVERY N-TH PIXEL IN-THE INPUT FRAME
C      (IN BOTH X AND Y)
C      TO FORM ONE PIXEL IN THE OUTPUT FRAME
C
C      THIS PROGRAM WILL WORK FOR BOTH 1-D AND 2-D IMAGES.
C
C
C     PARAMETERS ARE:
C
C         1)  INPUT    THE INPUT IMAGE
C         2)  OUTPUT   THE IMAGE CREATED
C         3)  COMPRESS  THE SAMPLING RATE IN PIXELS
C
C
C--
      INTEGER*4 AXISIN(2),AXISOU(2),STATUS,PIN,POUT,FACTOR
      INCLUDE 'INTERIM(FMTPAR)'
      INCLUDE 'INTERIM(ERRPAR)'
C
C      FIRST GET THE COMPRESSION FACTOR,WHICH MUST BE AN INTEGER
C
      FACTOR=1
  100 CALL RDKEYI('COMPRESS',.TRUE.,1,FACTOR,I,STATUS)
      IF(STATUS.GT.1.OR.I.GT.1.OR.FACTOR.LT.1) THEN
            CALL WRERR('DAMN')
            CALL CNPAR('COMPRESS',STATUS)
            IF (STATUS.GT.0) THEN
               CALL EXIT
            ELSE
               GO TO 100
            END IF
      END IF
C
C      NOW GET INPUT IMAGE
C
      CALL RDIMAG('INPUT',FMT_R,2,AXISIN,MAX,PIN,STATUS)
      IF (STATUS.NE.0.OR.MAX.GT.2) THEN
            CALL WRERR('BLAST')
            CALL EXIT
      END IF
C
C      GIVEN FACTOR AND AXISIN WE CAN COMPUTE THE
C      SIZE OF THE OUTPUT IMAGE
C
      AXISOU(1)=AXISIN(1)/FACTOR
      AXISOU(2)=AXISIN(2)/FACTOR
      IF(AXISOU(2).LT.1) AXISOU(2)=1
      CALL WRIMAG('OUTPUT',FMT_R,AXISOU,MAX,POUT,STATUS)
      IF (STATUS.NE.0) THEN
         CALL WRERR('HELL')
         CALL EXIT
      END IF
C
C      NOW WE HAVE PARAMETERS , WE CALL ALGORITHM
C
      CALL COMPICK(%VAL(PIN),AXISIN(1),AXISIN(2),
     1             %VAL(POUT),AXISOU(1),AXISOU(2),FACTOR)
      CALL FRDATA(' ',STATUS)
      CALL EXIT
      END
      SUBROUTINE COMPICK(IN,NX,NY,OUT,MX,MY,FACTOR)
C
C
C      THIS ROUTINE PICKS ONE PIXEL FROM A BLOCK
C      OF SIZE FACTOR*FACTOR
C      PIXELS IN ARRAY "IN" , WHOSE SIZE IS NX BY NY
C      AND PUTS THE RESULTS INTO THE OUTPUT ARRAY "OUT" OF
C      SIZE MX BY MY
C
C      ALL THE PARAMETERS ARE OF TYPE INTEGER*4
C
C      WRITTEN BY K F HARTLEY , BASED ON THE SAME ROUTINE
C      IN RGODR , WRITTEN BY C D PIKE
C
C--
      INTEGER FACTOR
      REAL IN(NX,NY),OUT(MX,MY)
C
      DO 200 J=1,MY
         JIN=(J-1)*FACTOR+1
         DO 100 I=1,MX
            IIN=(I-1)*FACTOR+1
            OUT(I,J)=IN(IIN,JIN)
  100    CONTINUE
  200 CONTINUE
      RETURN
      END
