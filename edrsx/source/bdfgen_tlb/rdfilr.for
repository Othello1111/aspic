      SUBROUTINE RDFILR(UNIT,VALS,MAXVAL,NVAL,ILEVEL,IERR)
*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*      TO READ MAXVAL REAL VALUES FROM A FILE ATTACHED TO UNIT 'UNIT'.
*
*SOURCE
*       RDFILR.FOR in BDFGEN.TLB
*
*METHOD
*      A RECORD IS READ FROM THE FILE AS A CHARACTER STRING. A CALL
*       IS THEN MADE TO RDSTRR TO EXTRACT ANY REAL VALUES FROM THE
*       CHARACTER STRING. IF THE LINE IS ALL COMMENT OR ALL BLANKS
*       THE NEXT RECORD IS READ. IF ANY ERRORS OCCUR SUCH AS AN
*       END OF FILE, UNTRANSLATABLE FIELD OR READ ERROR, EXECUTION
*       CONTINUES WITH IERR SET TO AN APPROPRIATE VALUE. IF THE
*       RECORD CONTAINS LESS THAN MAXVAL REAL FIELDS, THEN IERR IS
*       SET AND NVAL RETURNS THE NUMBER OF REAL VALUES ACTUALLY FOUND.
*
*ARGUMENTS
*       UNIT (IN)
*       INTEGER
*              THE LOGICAL UNIT NUMBER TO READ THE FILE ON
*       VALS (OUT)
*       REAL(MAXVAL)
*              AN ARRAY TO HOLD THE REAL VALUES READ FROM THE FILE
*       MAXVAL (IN)
*       INTEGER
*              THE MAXIMUM NUMBER OF VALUES TO READ. LESS WILL BE READ
*              IF AN UNTRANSLATABLE FIELD IS FOUND OR LESS THAN MAXVAL
*              VALUES EXIST IN THE RECORD
*       NVAL (OUT)
*       INTEGER
*              THE NUMBER OF REAL VALUES ACTUALLY READ
*       ILEVEL (IN)
*       INTEGER
*              IF ILEVEL IS 1 OR MORE THEN A MESSSAGE IS PRODUCED
*              IF AN UNTRANSLATABLE FIELD IS ENCOUNTERED, IDENTIFYING
*              THE FIELD
*       IERR (OUT)
*       INTEGER
*              ERROR FLAG: 0 - SUCCESS
*                          -1 - END OF FILE ENCOUNTERED
*                          -2 - LESS THAN MAXVAL VALUES FOUND
*                          -3 - AN UNTRANSLATABLE FIELD FOUND
*              IF AN ERROR OCCURS READING THE FILE THEN IERR WILL
*              BE SET TO A POSITIVE NUMBER EQUAL TO THE NUMBER OF
*              THE FORTRAN RUN TIME ERROR WHICH OCCURRED
*
*CALLS
*       DSB PROGS:
*              RDSTRR
*
*WRITTEN BY
*       D.S. BERRY
*----------------------------------------------------------------------
C
C
      INTEGER UNIT
      REAL VALS(MAXVAL)
      CHARACTER BUFFER*130
C
C READ IN A RECORD FROM THE FILE AS A CHARACTER STRING
C
   10 READ(UNIT,'(A)',IOSTAT=IERR) BUFFER
C
C IF AN ERROR OCCURED OR AN END OF FILE WAS MET THEN SKIP TO END
C
      IF(IERR.EQ.0) THEN
C
C IF BUFFER READ SUCCESSFULLY THEN CALL RDSTRR TO EXTRACT THE REAL
C VALUES FROM THE BUFFER
C
         CALL RDSTRR(BUFFER,VALS,MAXVAL,NVAL,ILEVEL,IERR)
C
C IF LESS THAN MAXVAL VALUES READ,
C
         IF(IERR.EQ.1) THEN
C
C AND NO VALUES AT ALL WERE READ, THEN TRY NEXT RECORD
C
            IF(NVAL.EQ.0) GOTO 10
C
C IF SOME RECORDS WERE READ THEN SET IERR
C
            IERR=-2
C
C IF AN UNTRANSLATABLE FIELD WAS MET THEN SET IERR
C
         ELSE IF(IERR.EQ.2) THEN
            IERR=-3
         ENDIF
      ENDIF
C
C FINISH
C
      RETURN
      END
