      SUBROUTINE CRB_STARFI
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO DETERMINE THE PARAMETERS OF A MODEL STAR PROFILE BY FITTING
*	STAR IMAGES
*
*METHOD
*	OBTAIN INPUT IMAGE CONTAINING STARS. OBTAIN LIST OF STAR
*	POSITIONS. OBTAIN WORKSPACE AND EXTRACT THE POSITIONS FROM THE
*	LIST. OBTAIN PARAMETERS CONTROLLING THE FITTING, THEN CALL
*	SPARAM TO PERFORM THE FITTING AND RETURN THE PROFILE PARAMETERS.
*	WRITE THE RESULTS TO THE ENVIRONMENT.
*
*ARGUMENTS
*	NONE
*
*STARLINK PARAMETERS
*	ILEVEL
*		INTERACTION LEVEL: CONTROLS PRINTING OF RESULTS
*	IMAGE
*		THE INPUT IMAGE
*	INPUT
*		THE INPUT X,Y LIST OF STAR POSITIONS
*	NOSPACE/ERROR/
*		ACCESSED IF WORKSPACE CANNOT BE OBTAINED
*	ISIZE
*		LENGTH OF THE SEARCH SQUARE SIDE TO BE USED IN LOCATING
*		THE STARS AND FINDING THEIR ELLIPTICITY
*	RANGE
*		RADIUS IN UNITS OF THE STAR 'SIGMA' OUT TO WHICH THE
*		RADIAL STAR PROFILE IS TO BE FITTED
*	NOSTARS/ERROR/
*		ACCESSED IF NO STARS COULD BE FOUND
*	NOFIT/ERROR/
*		ACCESSED IF THE RADIAL PROFILE COULD NOT BE FITTED
*	SEEING
*		OUTPUT PARAMETER: THE FWHM ACROSS THE STARS' MINOR AXIS
*	AXISR
*		OUTPUT PARAMETER: THE AXIS RATIO OF THE STAR IMAGES
*	THETA
*		OUTPUT PARAMETER: THE INCLINATION OF THE MAJOR AXIS TO
*		THE X AXIS IN DEGREES (X THROUGH Y POSITIVE)
*	GAMMA
*		OUTPUT PARAMETER: THE EXPONENT IN THE RADIAL PROFILE
*	DEVICE
*		SELECTS 'NONE' OR THE OUTPUT GRAPHICS DEVICE:
*		ARGS,TEKTRONIX,GOC,T4014 OR VERSATEC.
*
*CALLS
*	THIS PACKAGE:
*		GETPAR,GT2DIR,GTDSCR,GTXYLR,EXTLST,SPARAM,NEWSCL,GETCMD
*	STARLINK:
*		GETDYN,WRERR,WRKEYR,FRDATA
*
*NOTES
*	USES VAX %VAL FACILITY
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER CVAL*1,DEVICE*20
      REAL AXISR(1),THETA(1),FWHM(1),GAMMA(1)
C
C OBTAIN INTERACTION LEVEL
C
      ILEVEL=2
      CALL GETPAR('ILEVEL','INTEGER',1,1.0,3.0,.TRUE.,ILEVEL,RVAL,
     +IERR)
C
C OBTAIN INPUT IMAGE FRAME
C
      CALL GT2DIR('IMAGE',102,.FALSE.,NPIX,NLINES,IPIMG,IERRIM)
      IF(IERRIM.EQ.0) THEN
C
C IMAGE OBTAINED SUCCESSFULLY...EXTRACT REQUIRED DESCRIPTOR ITEMS
C
	INVAL=-100000
	SCALE=1.0
	CALL GTDSCR('IMAGE','INVAL','INTEGER',INVAL,RVAL,CVAL,IERR)
	CALL GTDSCR('IMAGE','BSCALE','REAL',IVAL,SCALE,CVAL,IERR)
C
C IF THE SCALE FACTOR IS NEGATIVE, THE IMAGE DATA IS STORED UPSIDE
C DOWN... OBTAIN NEW IMAGE FRAME AND INVERT IT
C
	IF(SCALE.LT.0.0) THEN
	  CALL GETDYN('NEWIMG',102,NPIX*NLINES,IPNEW,ISTNEW)
C
C IF SPACE IS NOT AVAILABLE, GIVE MESSAGE AND ABORT
C
	  IF(ISTNEW.NE.0) THEN
	    CALL WRERR('NOSPACE')
	    GO TO 99
	  ENDIF
C
C CALL NEWSCL TO INVERT THE SCALE FACTOR
C
	  CALL NEWSCL(%VAL(IPIMG),NPIX,NLINES,INVAL,SCALE,0.0,INVAL,
     +	  -SCALE,0.0,%VAL(IPNEW))
C
C SUBSEQUENTLY USE THE NEW IMAGE
C
	  SCALE=-SCALE
	  IPIMG=IPNEW
	ENDIF
C
C OBTAIN LIST OF INITIAL STAR POSITIONS
C
	CALL GTXYLR('INPUT',.FALSE.,NITEM,LSTLEN,IPIN,IERRXY)
	IF(IERRXY.EQ.0) THEN
C
C POSITION LIST OBTAINED SUCCESSFULLY...OBTAIN WORKSPACE FOR THE
C X,Y POSITIONS AND IDENTIFIERS
C
          CALL GETDYN('ID',104,5*LSTLEN,IPID,ISTID)
	  CALL GETDYN('X',104,LSTLEN,IPX,ISTX)
	  CALL GETDYN('Y',104,LSTLEN,IPY,ISTY)
	  CALL GETDYN('WORK',204,5*LSTLEN,IPWRK,ISTWRK)
C
C IF SPACE WAS NOT AVAILABLE, GIVE ERROR MESSAGE AND ABORT
C
	  IF((ISTID.NE.0).OR.(ISTX.NE.0).OR.(ISTY.NE.0).OR.
     +	  (ISTWRK.NE.0)) THEN
	    CALL WRERR('NOSPACE')
	    GO TO 99
	  ENDIF
C
C COPY IDENTIFIERS AND X,Y POSITIONS TO WORKSPACE
C
	  CALL EXTLST(%VAL(IPIN),NITEM,LSTLEN,%VAL(IPID),1,20)
	  CALL EXTLST(%VAL(IPIN),NITEM,LSTLEN,%VAL(IPX),21,24)
	  CALL EXTLST(%VAL(IPIN),NITEM,LSTLEN,%VAL(IPY),25,28)
C
C OBTAIN SEARCH AREA SIZE (ISIZE) AND THE RANGE OF RADII TO BE
C USED IN THE PROFILE FIT (RANGE,IN UNITS OF SIGMA)
C
	  ISIZE=15
	  CALL GETPAR('ISIZE','INTEGER',1,3.0,101.0,.TRUE.,ISIZE,
     +	  RVAL,IERR)
	  RANGE=4.0
	  CALL GETPAR('RANGE2','REAL',1,1.0,10.0,.TRUE.,IVAL,RANGE,
     +	  IERR)
C
C OBTAIN OUTPUT GRAPHICS DEVICE
C
	  IDEV=1
	  CALL GETCMD('DEVICE2','NONE,ARGS,TEKTRONIX,GOC,T4014,VERSATEC.',
     +	  1,IDEV,DEVICE,LDEV,IERR)
	  IDEV=IDEV-1
C
C CALL SPARAM TO FIND THE MEAN STAR PROFILE PARAMETERS
C
	  CALL SPARAM(%VAL(IPIMG),NPIX,NLINES,INVAL,ISIZE,RANGE,
     +	  %VAL(IPID),%VAL(IPX),%VAL(IPY),LSTLEN,ILEVEL,IDEV,AXISR(1),
     +	  THETA(1),FWHM(1),GAMMA(1),%VAL(IPWRK),IERR)
C
C IF IERR IS NOT ZERO, NO FIT HAS BEEN OBTAINED... GIVE ERROR MESSAGE
C AND ABORT
C
	  IF(IERR.EQ.1) THEN
	    CALL WRERR('NOSTARS')
	    GO TO 99
	  ELSE IF(IERR.GE.2) THEN
	    CALL WRERR('NOFIT')
	    GO TO 99
	  ENDIF
C
C WRITE RESULTS TO THE ENVIRONMENT
C
	  THETA(1)=THETA(1)*57.29578
	  CALL WRKEYR('SEEING',FWHM,1,ISTAT)
	  CALL WRKEYR('AXISR2',AXISR,1,ISTAT)
	  CALL WRKEYR('THETA',THETA,1,ISTAT)
	  CALL WRKEYR('GAMMA',GAMMA,1,ISTAT)
	ENDIF
      ENDIF
C
C RELEASE DATA AREAS AND RETURN
C
   99 CALL FRDATA(' ',ISTAT)
	CALL CNPAR('INPUT',ISTAT)
	CALL CNPAR('AXISR',ISTAT)
	CALL CNPAR('SEEING',ISTAT)
	CALL CNPAR('THETA',ISTAT)
	CALL CNPAR('GAMMA',ISTAT)
      RETURN
      END
