	SUBROUTINE ASP_COPCON(SRCFMT,DSTFMT,SRCBUF,DSTBUF,NVAL,STATUS)
C
C+	ASPIC ROUTINE TO PROVIDE COPYING/FORMAT CONVERSION BETWEEN
C	ANY FORMATS (NOT CHARACTER THOUGH)
C
C	EITHER THE ASPIC ROUTINE ASP_FMTCON OR THE STARLINK ONE
C	GEN_COPY IS CALLED, BUT NOTE THAT THE 'FMT' ARGUMENTS
C	ARE HERE AS LISTED IN 'FMTPAR', NOT AS USED IN ASP_FMTCON,
C	AND THAT NVAL IS A NUMBER OF VALUES, NOT OF BYTES
C
C	ARGUMENTS
C		SRCFMT	FORMAT OF SOURCE (INTEGER)
C		DSTFMT	FORMAT OF DESTINATION (INTEGER)
C		SRCBUF	SOURCE (NON-CHARACTER ARRAY)
C		DSTBUF	DESTINATION (NON-CHARACTER ARRAY)
C		NVAL	NUMBER OF ELEMENTS IN SRC AND DST (INTEGER)
C		STATUS	STATUS CODE (INTEGER)
C
	IMPLICIT INTEGER (A-Z)
	INCLUDE 'INTERIM(ERRPAR)'
	INCLUDE 'INTERIM(FMTPAR)'
	BYTE SRCBUF(*),DSTBUF(*)
C
C	DECODE THE FORMAT CODES USING STL_VALFMT AND STL_BSTCON
C
	CALL STL_VALFMT(SRCFMT,STATUS)
	IF (STATUS.NE.ERR_NORMAL) THEN
		RETURN
	ENDIF
	CALL STL_BSTCON(SRCFMT,I,J,SRCTYP)
	CALL STL_VALFMT(DSTFMT,STATUS)
	IF (STATUS.NE.ERR_NORMAL) THEN
		RETURN
	ENDIF
	CALL STL_BSTCON(DSTFMT,I,J,DSTTYP)
C
C	NOW DECIDE WHICH ROUTINE TO CALL (AND DO SO)
C
	IF (DSTTYP.EQ.SRCTYP) THEN
		NBYTES=NVAL*(SRCFMT-100*(SRCFMT/100))
		DO I=1,NBYTES,65535
			CALL GEN_COPY(SRCBUF(I),DSTBUF(I),
	1			MIN(NBYTES+1-I,65535))
		ENDDO
	ELSE
		CALL ASP_FMTCON(SRCTYP,DSTTYP,SRCBUF,DSTBUF,NVAL,I)
	ENDIF
C
C	AND RETURN
C
	RETURN
	END
