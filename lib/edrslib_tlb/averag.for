      SUBROUTINE AVERAG
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO FIND THE AVERAGE VALUE OF ALL VALID PIXELS IN AN IMAGE
*
*METHOD
*	OBTAIN THE IMAGE AND DESCRIPTOR ITEMS, SUM THE VALID PIXELS
*	AND FORM THE MEAN. WRITE RESULT TO ENVIRONMENT.
*
*ARGUMENTS
*	NONE
*
*STARLINK PARAMETERS
*	ILEVEL
*		INTERACTION LEVEL: PRINTS RESULTS IF ILEVEL>1
*	INPUT
*		INPUT IMAGE FRAME
*	AVERAGE
*		OUTPUT PARAMETER: AVERAGE VALUE
*	NGOOD
*		OUTPUT PARAMETER: NUMBER OF VALID PIXELS (INTEGER)
*	NOPIXELS/ERROR/
*		CALLED IF THERE ARE NO VALID PIXELS
*
*CALLS
*	THIS PACKAGE:
*		GETPAR,GT2DIR,GTDSCR,IMGSUM,LBGONE.
*	STARLINK:
*		WRKEYR,WRUSER,WRERR,FRDATA.
*
*NOTES
*	USES VAX %VAL FACILITY
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER CVAL*1,PRBUF*50
      REAL AV(1)
      INTEGER NPT(1)
C
C OBTAIN INTERACTION LEVEL
C
      ILEVEL=2
      CALL GETPAR('ILEVEL','INTEGER',1,1.0,3.0,.TRUE.,ILEVEL,RVAL,IERR)
C
C OBTAIN INPUT IMAGE FRAME
C
      CALL GT2DIR('INPUT',102,.FALSE.,NPIX,NLINES,IPIN,IERRIN)
      IF(IERRIN.EQ.0) THEN
C
C INPUT IMAGE OBTAINED SUCCESSFULLY... EXTRACT INVALID FLAG, SCALE
C AND ZERO LEVEL FROM DESCRIPTOR
C
	INVAL=-100000
	SCALE=1.0
	ZERO=0.0
	CALL GTDSCR('INPUT','INVAL','INTEGER',INVAL,RVAL,CVAL,IERR)
	CALL GTDSCR('INPUT','BSCALE','REAL',IVAL,SCALE,CVAL,IERR)
	CALL GTDSCR('INPUT','BZERO','REAL',IVAL,ZERO,CVAL,IERR)
C
C CALL IMGSUM TO SUM THE VALID IMAGE PIXELS
C
	CALL IMGSUM(%VAL(IPIN),NPIX,NLINES,INVAL,SUM,NPT(1))
C
C IF THE IMAGE HAS AT LEAST 1 VALID PIXEL, FIND THE AVERAGE
C
	IF(NPT(1).GE.1) THEN
	  AV(1)=(SUM/NPT(1))*SCALE+ZERO
C
C WRITE RESULT TO THE ENVIRONMENT
C
	  CALL WRKEYR('AVERAGE',AV,1,ISTAT)
	  CALL WRKEYI('NGOOD',NPT,1,ISTAT)
C
C IF ILEVEL IS 2 OR MORE, PRINT THE RESULT FOR THE USER
C
	  IF(ILEVEL.GE.2) THEN
	    WRITE(PRBUF,10) NPT(1),AV(1)
   10	    FORMAT(' AVERAGE OF ',I8,' PIXEL(S) IS ',SS,G13.6)
C
C REMOVE UNWANTED BLANKS IN MESSAGE
C
	    CALL LBGONE(PRBUF(13:))
	    CALL WRUSER(' ',ISTAT)
	    CALL WRUSER(PRBUF,ISTAT)
	    CALL WRUSER(' ',ISTAT)
	  ENDIF
C
C IF THE IMAGE HAS NO VALID PIXELS... GIVE MESSAGE AND ABORT
C
	ELSE
	  CALL WRERR('NOPIXELS')
	ENDIF
      ENDIF
C
C RELEASE IMAGE FRAME AND RETURN
C
      CALL FRDATA(' ',ISTAT)
      RETURN
      END
