      SUBROUTINE CMDWRD(LIST,COMMND,NINPUT,NCOMM,IDCOMM,
     +                  LENCOM,IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO COMPARE A CHARACTER PARAMETER WITH A LIST OF POSSIBLE
*	ALTERNATIVES TO CHECK ITS VALIDITY
*
*METHOD
*	EXTRACT EACH ALTERNATIVE FROM THE LIST AND MATCH IT AGAINST
*	THE PARAMETER. COUNT THE NUMBER OF CHARACTER MATCHES WHICH
*	OCCUR. DECIDE WHETHER THE PARAMETER IS UNIQUELY IDENTIFIED,
*	ABBREVIATED, AMBIGUOUSLY ABBREVIATED, MIS-TYPED BY UP TO 1
*	CHARACTER OR NOT IDENTIFIED AT ALL
*
*ARGUMENTS
*	LIST (IN)
*	CHARACTER*(*)
*		INPUT LIST OF ALTERNATIVES, SEPARATED BY COMMAS AND
*		TERMINATED WITH A FULL-STOP
*	COMMND (IN)
*	CHARACTER*(*)
*		THE PARAMETER TO BE IDENTIFIED
*	NINPUT (OUT)
*	INTEGER
*		THE NUMBER OF NON-BLANK CHARACTERS IN COMMND USED TO
*		IDENTIFY IT
*	NCOMM (OUT)
*	INTEGER
*		THE POSITION OF THE IDENTIFIED PARAMETER IN THE LIST
*		OF ALTERNATIVES
*	IDCOMM (OUT)
*	CHARACTER*(*)
*		THE FULL FORM OF THE IDENTIFIED PARAMETER
*	LENCOM (OUT)
*	INTEGER
*		THE NUMBER OF NON-BLANK CHARACTERS IN IDCOMM
*	IERR (OUT)
*	INTEGER
*		INDICATES THE SUCCESS OF THE IDENTIFICATION
*		0: UNIQUELY IDENTIFIED, OR A UNIQUE ABBREVIATION
*		1: IDENTIFIED WITH 1 MIS-TYPED CHARACTER
*		2: AMBIGUOUS ABBREVIATION
*		3: NOT IDENTIFIED
*
*CALLS
*	THIS PACKAGE:
*		LBGONE,NINDEX,NMATCH
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER LIST*(*),COMMND*(*),IDCOMM*(*)
      INTEGER START,END,ENDLST
C
C REMOVE LEADING BLANKS FROM INPUT STRING
C
      CALL LBGONE(COMMND)
C
C FIND THE NUMBER OF CHARACTERS TO THE FIRST BLANK OR COMMA
C
      NINPUT=MIN(NINDEX(COMMND,' ',1),NINDEX(COMMND,',',1))-1
C
C IF THERE ARE NO NON-BLANK CHARACTERS, ABORT WITH IERR=4
C
      IF(NINPUT.LE.0) THEN
        IERR=4
      ELSE
C
C FIND THE END OF THE LIST OF ALTERNATIVE VALUES
C
        ENDLST=NINDEX(LIST,'.',1)-1
C
C INITIALLISE COUNTERS FOR PERFECT, GOOD AND POSSIBLE IDENTIFICATIONS
C
        LISTN=0
        NPERF=0
        NGOOD=0
        NPOSS=0
C
C COUNT THROUGH THE ALTERNATIVES, FINDING THE START AND FINISH OF
C EACH WORD IN THE LIST
C
   16   LISTN=LISTN+1
        START=NINDEX(LIST,',',LISTN-1)+1
        END=MIN(NINDEX(LIST,',',LISTN)-1,ENDLST)
        LENGTH=END-START+1
C
C IF A WORD HAS BEEN FOUND, MATCH IT AGAINST THE INPUT STRING
C
        IF(START.LE.ENDLST) THEN
          IF(LENGTH.GE.1) THEN
            NC=NMATCH(LIST(START:END),COMMND(:NINPUT))
C
C IF IT MATCHES PERFECTLY, COUNT 1 PERFECT MATCH
C
            IF(NC.EQ.NINPUT.AND.NC.EQ.LENGTH) THEN
              NPERF=1
              IDCOMM=LIST(START:END)
              NCOMM=LISTN
              LENCOM=LENGTH
C
C IF IT IS AN ABBREVIATION AND NO PERFECT MATCHES HAVE BEEN
C FOUND, COUNT 1 GOOD MATCH
C
            ELSE IF(NC.EQ.NINPUT.AND.NC.NE.LENGTH.AND.
     +              NPERF.EQ.0) THEN
              NGOOD=NGOOD+1
              IDCOMM=LIST(START:END)
              NCOMM=LISTN
              LENCOM=LENGTH
C
C IF THERE IS 1 MIS-MATCHED CHARACTER, BUT NO BETTER MATCHES HAVE
C BEEN FOUND, COUNT 1 POSSIBLE MATCH
C
            ELSE IF(NC.EQ.NINPUT-1.AND.NPERF.EQ.0.AND.
     +              NGOOD.EQ.0) THEN
              NPOSS=NPOSS+1
              IDCOMM=LIST(START:END)
              NCOMM=LISTN
              LENCOM=LENGTH
            ENDIF
          ENDIF
          GO TO 16
        ENDIF
C
C IF 1 PERFECT MATCH WAS FOUND, INPUT IS UNIQUELY IDENTIFIED
C
        IF(NPERF.EQ.1) THEN
          IERR=0
C
C IF 1 GOOD MATCH FOUND, INPUT IS A UNIQUE ABBREVIATION
C
        ELSE IF(NGOOD.GT.0) THEN
          IF(NGOOD.EQ.1) THEN
            IERR=0
C
C IF 2 OR MORE GOOD MATCHES, INPUT IS AN AMBIGUOUS ABBREVIATION
C
          ELSE
            IERR=2
          ENDIF
C
C IF 1 POSSIBLE MATCH, INPUT WAS MIS-TYPED
C
        ELSE IF(NPOSS.EQ.1) THEN
          IERR=1
        ELSE
C
C IF 2 OR MORE POSSIBLE MATCHES, INPUT IS NOT IDENTIFIED
C
          IERR=3
        ENDIF
      ENDIF
C
C IF IDENTIFICATION WAS UNSUCCESSFUL, RETURN WITH A BLANK 
C IDENTIFICATION, OTHERWISE RETURN THE FULL FORM OF THE IDENTIFICATION
C
      IF(IERR.GT.1) THEN
        NCOMM=0
        IDCOMM=' '
        LENCOM=1
      ENDIF
      RETURN
      END
