      SUBROUTINE LINSTK(IA,NPIXA,NLINEA,INVALA,LINEA,IB,NPIXB,
     +			NIMAGE)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO COPY A SPECIFIED LINE FROM AN IMAGE INTO A STACK OF IMAGE
*	LINES TAKEN FROM ELSEWHERE
*
*METHOD
*	COPY THE SPECIFIED LINE TO A SPECIFIED STACK LOCATION. PAD THE
*	LINE WITH INVALID PIXELS TO MAKE UP ANY EXTRA LENGTH REQUIRED.
*	REPLACE IT WITH INVALID PIXELS IF THE LINE SPECIFIED LIES
*	OUTSIDE THE INPUT IMAGE
*
*ARGUMENTS
*	IA (IN)
*	INTEGER*2(NPIXA,NLINEA)
*		THE INPUT IMAGE
*	NPIXA,NLINEA (IN)
*	INTEGER
*		DIMENSIONS OF IA
*	INVALA (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IA
*	LINEA (IN)
*	INTEGER
*		SPECIFIES THE LINE TO BE COPIED FROM IA
*	IB (IN/OUT)
*	INTEGER*2(NPIXB,*)
*		THE STACK OF IMAGE LINES, EACH OF LENGTH NPIXB
*	NPIXB (IN)
*	INTEGER
*		THE LENGTH OF LINES IN IB
*	NIMAGE (IN)
*	INTEGER
*		THE LOCATION IN THE STACK IB INTO WHICH THE LINE IS
*		TO BE COPIED
*
*CALLS
*	NONE
*
*NOTES
*	USES INTEGER*2 ARRAYS
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      INTEGER*2 IA(NPIXA,NLINEA),IB(NPIXB,*)
C
C IF THE LINE REQUIRED LIES OUTSIDE THE INPUT IMAGE, FILL THE
C CORRESPONDING WORKSPACE WITH INVALID PIXEL VALUES
C
      IF(LINEA.GT.NLINEA) THEN
	DO 1 I=1,NPIXB
	  IB(I,NIMAGE)=INVALA
    1	CONTINUE
      ELSE
C
C OTHERWISE COPY THE REQUIRED LINE TO THE WORKSPACE
C
	DO 2 I=1,NPIXB
C
C IF THE WORKSPACE HAS LONGER LINES THAN THE IMAGE, PAD OUT WITH
C INVALID PIXEL VALUES
C
	  IF(I.LE.NPIXA) THEN
	    IB(I,NIMAGE)=IA(I,LINEA)
	  ELSE
	    IB(I,NIMAGE)=INVALA
	  ENDIF
    2	CONTINUE
      ENDIF
      RETURN
      END
