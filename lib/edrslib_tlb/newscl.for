      SUBROUTINE NEWSCL(IA,NPIX,NLINES,INVALA,ASCALE,AZERO,
     +			INVALB,BSCALE,BZERO,IB)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO ALTER THE INTEGERS IN AN IMAGE TO SUIT A NEW SCALE FACTOR
*	AND ZERO LEVEL AND A NEW INVALID PIXEL FLAG
*
*METHOD
*	ALTER EACH INTEGER SO THAT IT HAS THE SAME DATA VALUE WITH THE
*	NEW ITEMS
*
*ARGUMENTS
*	IA (IN)
*	INTEGER*2(NPIX*NLINES)
*		THE INPUT IMAGE
*	NPIX,NLINES (IN)
*	INTEGER
*		THE DIMENSIONS OF IA,IB
*	INVALA (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IA
*	ASCALE,AZERO (IN)
*	REAL
*		SCALE FACTOR AND ZERO LEVEL FOR IA
*	INVALB (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IB
*	BSCALE,BZERO (IN)
*	REAL
*		SCALE FACTOR AND ZERO LEVEL FOR IB
*	IB (OUT)
*	INTEGER*2(NPIX*NLINES)
*		OUTPUT IMAGE
*
*CALLS
*	NONE
*
*NOTES
*	USES INTEGER*2 ARRAYS
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      INTEGER*2 IA(NPIX*NLINES),IB(NPIX*NLINES)
C
C CALCULATE CONVERSION FACTORS FROM INPUT TO OUTPUT
C
      NN=NPIX*NLINES
      BS=BSCALE
      IF(ABS(BS).LT.1.0E-20) BS=SIGN(1.0E-20,BS)
      ZERO=(AZERO-BZERO)/BS
      SCALE=ASCALE/BS
C
C SCAN IMAGE
C
      DO 1 I=1,NN
C
C IF INPUT PIXEL IS INVALID, SO IS OUTPUT
C
	IF(IA(I).EQ.INVALA) THEN
	  IB(I)=INVALB
	ELSE
C
C CALCULATE OUTPUT INTEGER
C
	  B=IA(I)*SCALE+ZERO
C
C OUTPUT IS INVALID IF OVERFLOW OCCURS
C
	  IF(ABS(B).LE.32767.0) THEN
	    IB(I)=NINT(B)
	  ELSE
	    IB(I)=INVALB
	  ENDIF
	ENDIF
    1 CONTINUE
      END
