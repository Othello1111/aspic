      SUBROUTINE LINEFI
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO GENERATE AN IMAGE, EACH LINE OF WHICH IS A CONSTANT OR
*	STRAIGHT LINE FIT TO THE CORRESPONDING INPUT IMAGE LINE
*
*METHOD
*	OBTAIN INPUT IMAGE AND EXTRACT DESCRIPTOR ITEMS. DETERMINE
*	THE METHOD OF FITTING THE INPUT LINES, THEN OBTAIN THE
*	OUTPUT IMAGE. CALL LINFIL TO FIT THE LINES. PRINT MESSAGE
*	IF ANY LINES WERE NOT FITTED. UPDATE OUTPUT DESCRIPTOR.
*
*ARGUMENTS
*	NONE
*
*STARLINK PARAMETERS
*	ILEVEL
*		INTERACTION LEVEL: CONTROLS PRINTING OF MESSAGES
*	INPUT
*		INPUT IMAGE
*	METHOD
*		METHOD OF FITTING THE INPUT LINES
*	MINPIX
*		MIMINUM NUMBER OF VALID PIXELS REQUIRED IN EACH LINE
*	OUTPUT
*		OUTPUT IMAGE
*	TITLE
*		TITLE TO REPLACE INPUT TITLE IN OUTPUT IMAGE
*
*CALLS
*	THIS PACKAGE:
*		GETPAR,GT2DIR,GTDSCR,GETCMD,GT2DIW,LINFIL,LBGONE,PTDSCR
*	STARLINK:
*		WRUSER,RDKEYC,CYDSCR,FRDATA
*
*NOTES
*	USES VAX %VAL FACILITY
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER TITLE(1)*30,CVAL*1,METHOD*10,PRBUF*80
C
C OBTAIN INTERACTION LEVEL
C
      ILEVEL=2
      CALL GETPAR('ILEVEL','INTEGER',1,1.0,3.0,.TRUE.,ILEVEL,RVAL,IERR)
C
C OBTAIN INPUT IMAGE FRAME
C
      CALL GT2DIR('INPUT',102,.FALSE.,NPIX,NLINES,IPIN,IERRIN)
      IF(IERRIN.EQ.0) THEN
C
C INPUT IMAGE OBTAINED SUCCESSFULLY...EXTRACT DESCRIPTOR ITEMS
C
        TITLE(1)=' '
        INVALA=-100000
	CALL GTDSCR('INPUT','TITLE','CHARACTER',IVAL,RVAL,TITLE(1),IERR)
        CALL GTDSCR('INPUT','INVAL','INTEGER',INVALA,RVAL,CVAL,IERR)
C
C SET OUTPUT INVALID PIXEL FLAG
C
	IF(ABS(INVALA).LE.32767) THEN
	  INVALB=INVALA
	ELSE
	  INVALB=-32767
	ENDIF
C
C OBTAIN METHOD OF FITTING INPUT LINES AND THE MINIMUM NUMBER OF
C VALID PIXELS REQUIRED IN EACH LINE
C
	IMETH=1
	CALL GETCMD('METHOD','CONSTANT,LINEAR.',
     +  1,IMETH,METHOD,LMETH,IERR)
        NMIN=IMETH
	CALL GETPAR('MINPIX','INTEGER',1,REAL(IMETH),REAL(NPIX),
     +	.TRUE.,NMIN,RVAL,IERR)
C
C OBTAIN OUTPUT IMAGE
C
        CALL GT2DIW('OUTPUT',102,.FALSE.,NPIX,NLINES,IPOUT,IERROU)
	IF(IERROU.EQ.0) THEN
C
C OUTPUT IMAGE OBTAINED SUCCESSFULLY...CALL LINFIL TO PERFORM THE
C FITTING
C
	  CALL LINFIL(%VAL(IPIN),NPIX,NLINES,INVALA,IMETH,NMIN,INVALB,
     +	  NBAD,%VAL(IPOUT))
C
C IF SOME LINES WERE NOT FITTED, GIVE MESSAGE
C
          IF(NBAD.GT.0.AND.ILEVEL.GE.2) THEN
	    WRITE(PRBUF,66) NBAD
   66	    FORMAT('   ',I10,
     +	    ' IMAGE LINE(S) CONTAINED INSUFFICIENT DATA TO FIT')
	    CALL LBGONE(PRBUF(4:))
	    CALL WRUSER(' ',ISTAT)
	    CALL WRUSER(PRBUF,ISTAT)
	    CALL WRUSER(' ',ISTAT)
	  ENDIF
C
C OBTAIN OUTPUT TITLE AND UPDATE OUTPUT DESCRIPTOR
C
	  CALL RDKEYC('TITLE',.TRUE.,1,TITLE,NVAL,ISTAT)
	  CALL CYDSCR('INPUT','OUTPUT',ISTAT)
	  CALL PTDSCR('OUTPUT','TITLE','CHARACTER',IVAL,RVAL,TITLE(1),
     +	  IERR)
	  CALL PTDSCR('OUTPUT','INVAL','INTEGER',INVALB,RVAL,CVAL,IERR)
	ENDIF
      ENDIF
C
C FREE DATA AREAS AND RETURN
C
      CALL FRDATA(' ',ISTAT)
      END
