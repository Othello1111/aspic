      SUBROUTINE BININT(IA,NPIX,NLINES,INVAL,IDX,IDY,X,Y,Z,W,NBIN)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO BIN AN IMAGE INTO RECTANGULAR BINS
*
*METHOD
*	SCAN THROUGH THE BINS, AVERAGING THE VALID PIXELS IN EACH BIN.
*	FORM MEAN IMAGE VALUE,X AND Y POSITION FOR EACH BIN. FORM
*	WEIGHT PROPORTIONAL TO SQUARE ROOT OF NUMBER OF VALID PIXELS
*	IN EACH BIN.
*
*ARGUMENTS
*	IA (IN)
*	INTEGER*2(NPIX,NLINES)
*		INPUT IMAGE
*	NPIX,NLINES (IN)
*	INTEGER
*		IMAGE DIMENSIONS
*	INVAL (IN)
*	INTEGER
*		INVALID PIXEL FLAG
*	IDX,IDY (IN)
*	INTEGER
*		SIZE OF BINS IN PIXELS IN X AND Y DIRECTIONS
*	X,Y (OUT)
*	REAL(*)
*		LISTS OF MEAN X,Y POSITIONS FOR BINS
*	Z (OUT)
*	REAL(*)
*		MEAN VALUE FOR EACH BIN
*	W (OUT)
*	REAL(*)
*		WEIGHT FOR EACH BIN
*	NBIN (OUT)
*	INTEGER
*		NUMBER OF BINS CONTAINING 1 OR MORE VALID PIXELS
*
*CALLS
*	NONE
*
*NOTES
*	USES INTEGER*2 ARRAY
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      INTEGER*2 IA(NPIX,NLINES)
      DOUBLE PRECISION X(*),Y(*),Z(*),W(*)
C
C INITIALLISE COUNTER FOR NUMBER OF BINS CONTAINING VALID PIXELS
C
      NBIN=0
C
C SCAN THROUGH BINS, CALCULATING THE MIN AND MAX X AND Y COORDS.
C
      DO 6 JMIN=1,NLINES,IDY
	JMAX=MIN(JMIN+IDY-1,NLINES)
	DO 5 IMIN=1,NPIX,IDX
	  IMAX=MIN(IMIN+IDX-1,NPIX)
C
C INITIALLISE SUMS FOR FINDING MEAN X,Y AND DATA WITHIN EACH BIN
C
	  SUM=0.0
	  NPT=0
	  IX=0
	  IY=0
C
C SCAN THROUGH THE PIXELS WITHIN THE BIN, FORMING SUMS OF VALID PIXELS
C
	  DO 4 J=JMIN,JMAX
	    DO 3 I=IMIN,IMAX
	      IF(IA(I,J).NE.INVAL) THEN
		SUM=SUM+IA(I,J)
		NPT=NPT+1
		IX=IX+I
		IY=IY+J
	      ENDIF
    3	    CONTINUE
    4	  CONTINUE
C
C IF THERE WAS AT LEAST 1 VALID PIXEL IN THE BIN, FORM MEAN VALUES
C
	  IF(NPT.GE.1) THEN
	    NBIN=NBIN+1
	    X(NBIN)=REAL(IX)/NPT
	    Y(NBIN)=REAL(IY)/NPT
	    Z(NBIN)=SUM/NPT
	    W(NBIN)=SQRT(REAL(NPT))
	  ENDIF
    5	CONTINUE
    6 CONTINUE
      RETURN
      END
