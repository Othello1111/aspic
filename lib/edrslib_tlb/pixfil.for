      SUBROUTINE PIXFIL
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO REPLACE INVALID PIXELS IN AN IMAGE WITH NEW VALUES WHICH
*	MATCH THE SURROUNDING DATA
*
*METHOD
*	OBTAIN INPUT IMAGE. OBTAIN WORKSPACE. CALL FILLIN TO
*	REPLACE THE INVALID PIXELS BY SOLVING LAPLACE'S EQUATION IN
*	THE INVALID REGIONS. PRINT THE NUMBER OF PIXELS REPLACED AND
*	INFORMATION ABOUT THE ITERATIONS IN FILLIN.
*	UPDATE THE OUTPUT DESCRIPTOR.
*
*ARGUMENTS
*	NONE
*
*STARLINK PARAMETERS
*	ILEVEL
*		INTERACTION LEVEL: CONTROLS PRINTING OF RESULTS
*	INPUT
*		THE INPUT IMAGE
*		IMAGE
*	NOSPACE/ERROR/
*		ACCESSED IF WORKSPACE CANNOT BE OBTAINED
*	OUTPUT
*		THE OUTPUT IMAGE
*	NITER
*		THE NUMBER OF ITERATIONS REQUIRED
*	SIZE
*		INITIAL SMOOTHING SIZE
*	NONEVAL/ERROR/
*		ACCESSED IF THE INPUT IMAGE HAS NO VALID PIXELS
*	TITLE
*		A TITLE TO REPLACE THE INPUT TITLE IN THE OUTPUT IMAGE
*
*CALLS
*	THIS PACKAGE:
*		GETPAR,GT2DIR,GTDSCR,IMGSUM,GT2DIW,FILLIN,LBGONE,PTDSCR
*	STARLINK:
*		WRERR,GETDYN,WRUSER,RDKEYC,CYDSCR,FRDATA
*
*NOTES
*	USES VAX %VAL FACILITY
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER CVAL*1,TITLE(1)*30,PRBUF*80
C
C OBTAIN INTERACTION LEVEL
C
      ILEVEL=2
      CALL GETPAR('ILEVEL','INTEGER',1,1.0,3.0,.TRUE.,ILEVEL,RVAL,IERR)
C
C OBTAIN INPUT IMAGE
C
      CALL GT2DIR('INPUT',102,.FALSE.,NPIX,NLINES,IPIN,IERRIN)
      IF(IERRIN.EQ.0) THEN
C
C INPUT IMAGE OBTAINED SUCCESSFULLY...EXTRACT DESCRIPTOR ITEMS
C
	TITLE(1)=' '
	INVAL=-100000
        SCALE=1.0
	CALL GTDSCR('INPUT','TITLE','CHARACTER',IVAL,RVAL,TITLE(1),
     +	IERR)
	CALL GTDSCR('INPUT','INVAL','INTEGER',INVAL,RVAL,CVAL,IERR)
        CALL GTDSCR('INPUT','BSCALE','REAL',IVAL,SCALE,CVAL,IERR)
C
C OBTAIN WORKSPACE FOR FILLIN
C
	CALL GETDYN('DSUM',204,NPIX*NLINES,IPDS,ISTDS)
	CALL GETDYN('WTSUM',204,NPIX*NLINES,IPWTS,ISTWTS)
	CALL GETDYN('DLAST',204,NPIX,IPDL,ISTDL)
	CALL GETDYN('WTLAST',204,NPIX,IPWTL,ISTWTL)
C
C IF WORKSPACE WAS NOT AVAILABLE, GIVE MESSAGE AND ABORT
C
	ISTMAX=MAX(ISTDS,ISTWTS,ISTDL,ISTWTL)
	IF(ISTMAX.NE.0) THEN
	  CALL WRERR('NOSPACE')
	  GO TO 99
	ENDIF
C
C OBTAIN OUTPUT IMAGE FRAME
C
	CALL GT2DIW('OUTPUT',102,.FALSE.,NPIX,NLINES,IPOUT,IERROU)
	IF(IERROU.EQ.0) THEN
C
C OUTPUT IMAGE OBTAINED SUCCESSFULLY...GET THE NUMBER OF ITERATIONS
C REQUIRED
C
          NITER=2
	  CALL GETPAR('NITER','INTEGER',1,2.0,100.0,.TRUE.,NITER,
     +	  RVAL,IERR)
C
C OBTAIN THE INITIAL SMOOTHING SIZE
C
	  SIZE=5.0
	  CALL GETPAR('SIZE','REAL',1,0.1,1.0E6,.TRUE.,IVAL,SIZE,CVAL,
     +	  IERR)
C
C CALL FILLIN TO REPLACE THE INVALID PIXELS
C
	  CALL FILLIN(%VAL(IPIN),NPIX,NLINES,INVAL,SIZE,ILEVEL,CNGMAX,
     +	  CNGRMS,NITER,SCALE,
     +	  %VAL(IPOUT),NBAD,%VAL(IPDS),%VAL(IPWTS),%VAL(IPDL),
     +	  %VAL(IPWTL))
C
C IF THERE ARE NO VALID PIXELS, GIVE MESSAGE AND ABORT
C
          IF(NBAD.EQ.NPIX*NLINES) THEN
	    CALL WRERR('NONEVAL')
	    GO TO 99
	  ENDIF
C
C PRINT INFORMATIONAL MESSAGES
C
	  IF(ILEVEL.GE.2) THEN
	    WRITE(PRBUF,15)NITER
   15	    FORMAT('   ',I10,' ITERATION(S) COMPLETED')
	    CALL LBGONE(PRBUF(4:))
	    CALL WRUSER(' ',ISTAT)
	    CALL WRUSER(PRBUF,ISTAT)
	    WRITE(PRBUF,16)NBAD
   16	    FORMAT('   ',I10,' INVALID PIXEL(S) REPLACED')
	    CALL LBGONE(PRBUF(4:))
	    CALL WRUSER(' ',ISTAT)
	    CALL WRUSER(PRBUF,ISTAT)
	    WRITE(PRBUF,17)CNGMAX
   17	    FORMAT('   MAXIMUM CHANGE IN LAST ITERATION WAS ',SS,G13.6)
	    CALL WRUSER(' ',ISTAT)
	    CALL WRUSER(PRBUF,ISTAT)
	    CALL WRUSER(' ',ISTAT)
	    WRITE(PRBUF,518)CNGRMS
  518	    FORMAT('   RMS CHANGE IN LAST ITERATION WAS ',SS,G13.6)
	    CALL WRUSER(PRBUF,ISTAT)
	    CALL WRUSER(' ',ISTAT)
	    WRITE(PRBUF,18) SIZE
   18	    FORMAT('   FINAL SMOOTHING SIZE=',SS,G13.6)
	    CALL WRUSER(PRBUF,ISTAT)
	    CALL WRUSER(' ',ISTAT)
          ENDIF
C
C OBTAIN OUTPUT TITLE
C
	  CALL RDKEYC('TITLE',.TRUE.,1,TITLE,NVAL,ISTAT)
C
C UPDATE OUTPUT DESCRIPTOR ITEMS
C
	  CALL CYDSCR('INPUT','OUTPUT',ISTAT)
	  CALL PTDSCR('OUTPUT','TITLE','CHARACTER',IVAL,RVAL,
     +	  TITLE(1),IERR)
	  CALL PTDSCR('OUTPUT','INVAL','INTEGER',-100000,RVAL,CVAL,
     +	  IERR)
	ENDIF
      ENDIF
C
C RELEASE DATA AREAS AND RETURN
C
   99 CALL FRDATA(' ',ISTAT)
      END
