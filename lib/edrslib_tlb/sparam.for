      SUBROUTINE SPARAM(IA,NPIX,NLINES,INVAL,ISIZE,RANGE,ID,X,Y,NXY,
     +			ILEVEL,IDEV,AXISR,THETA,FWHM,GAMMA,SIG,IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO FIND A SET OF PARAMETERS DESCRIBING A MODEL STAR IMAGE
*	FITTED TO A SET OF STAR IMAGES AND TO DISPLAY THE RESULTS.
*
*METHOD
*	CALL STARIM TO DETERMINE THE ELLIPTICITY OF THE STAR IMAGES
*	PRINT HEADINGS AND A TABLE OF THE ELLIPSE PARAMETERS FOR
*	EACH STAR USED. PRINT THE MEAN ELLIPSE PARAMETERS.
*	CALL RADPRF TO DETERMINE THE MODEL RADIAL STAR PROFILE AND TO
*	PRINT THE RESULTS.
*
*ARGUMENTS
*	IA (IN)
*	INTEGER*2(NPIX,NLINES)
*		THE INPUT IMAGE CONTAINING THE STARS TO BE FITTED
*	NPIX,NLINES (IN)
*	INTEGER
*		THE DIMENSIONS OF IA
*	INVAL (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IA
*	ISIZE (IN)
*	INTEGER
*		LENGTH OF THE SEARCH SQUARE SIDE USED IN FINDING STARS
*		AND CALCULATING THEIR ELLIPTICITY
*	RANGE (IN)
*	REAL
*		THE RADIUS IN UNITS OF THE STAR 'SIGMA' OUT TO WHICH
*		THE RADIAL PROFILE IS FITTED
*	ID (IN)
*	BYTE(20,NXY)
*		A LIST OF ASCII IDENTIFIERS FOR THE STARS
*	X,Y (IN)
*	REAL(NXY)
*		THE APPROXIMATE POSITIONS OF THE STARS TO BE FITTED
*	NXY (IN)
*	INTEGER
*		THE NUMBER OF STARS TO BE USED
*	ILEVEL (IN)
*	INTEGER
*		INTERACTION LEVEL: CONTROLS PRINTING OF RESULTS
*	IDEV (IN)
*	INTEGER
*		SELECTS GRAPHICS DEVICE:
*			0: NONE
*			1: ARGS
*			2: TEKTRONIX
*			3: GOC
*			4: T4014
*			5: VERSATEC
*	AXISR (OUT)
*	REAL
*		THE AXIS RATIO OF THE STAR IMAGES
*	THETA (OUT)
*	REAL
*		THE INCLINATION OF THE MAJOR AXIS OF THE STAR IMAGES TO
*		THE X DIRECTION IN RADIANS (X THROUGH Y POSITIVE)
*	FWHM (OUT)
*	REAL
*		THE FULL WIDTH AT HALF MAXIMUM OF THE STAR IMAGES IN
*		THE MINOR AXIS DIRECTION
*	GAMMA (OUT)
*	REAL
*		THE EXPONENT IN THE RADIAL STAR PROFILE
*	SIG (WORKSPACE)
*	REAL(NXY,5)
*		INTERMEDIATE STORAGE FOR THE WIDTHS OF THE STAR MARGINAL
*		PROFILES
*	IERR (OUT)
*	INTEGER
*		ERROR FLAG: ZERO FOR SUCCESS
*
*CALLS
*	THIS PACKAGE:
*		STARIM,ELLIPS,LBGONE,RADPRF
*	STARLINK:
*		WRUSER
*
*NOTES
*	USES INTEGER*2 AND BYTE ARRAYS
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      INTEGER*2 IA(NPIX,NLINES)
      BYTE ID(20,NXY)
      REAL X(NXY),Y(NXY),SIG(NXY,5),PRF(4)
      CHARACTER IDCHR*20,PRBUF*80
C
C CALL STARIM TO DETERMINE THE MEAN ELLIPTICITY OF THE STAR IMAGES
C
      IERR=0
      CALL STARIM(IA,NPIX,NLINES,INVAL,X,Y,NXY,ISIZE,SIG0,AXISR,THETA,
     +NGOOD,SIG)
C
C IF NO STARS COULD BE FOUND TO DETERMINE THE ELLIPTICITY, SET IERR=1
C AND ABORT
C
      IF(NGOOD.LE.0) THEN
	IERR=1
	GO TO 99
      ENDIF
C
C IF ILEVEL.GE.3 PRINT A TABLE OF THE AXIS RATIOS OF EACH STAR USED
C
      IF(ILEVEL.GE.3) THEN
C
C PRINT HEADINGS
C
	CALL WRUSER(' ',ISTAT)
	WRITE(PRBUF,10)
   10   FORMAT(T52,'GAUSSIAN',T67,'AXIS RATIO')
	CALL WRUSER(PRBUF,ISTAT)
        WRITE(PRBUF,11)
   11	FORMAT(5X,'IDENTIFIER',T24,' X COORD.',5X,' Y COORD.',4X,
     +  'FWHM SEEING',5X,'/ ANGLE (DEG)')
	CALL WRUSER(PRBUF,ISTAT)
	WRITE(PRBUF,12)
   12	FORMAT(5X,'----------',T24,' --------',5X,' --------',4X,
     +  '-----------',5X,'-------------')
	CALL WRUSER(PRBUF,ISTAT)
C
C CONSIDER EACH STAR
C
	DO 101 I=1,NXY
C
C EXTRACT ITS IDENTIFIER
C
	  DO 104 L=1,20
	    NCHAR=ID(L,I)
	    IDCHR(L:L)=CHAR(NCHAR)
  104	  CONTINUE
C
C IF THE WIDTHS OF THE MARGINAL PROFILES WERE FOUND, CALL ELLIPS
C TO FIND THE PARAMETERS SPECIFYING THE STAR SHAPE
C
	  IF(SIG(I,5).GT.1.0E-10) THEN
	    PRF(1)=SIG(I,1)
	    PRF(2)=SIG(I,2)
	    PRF(3)=SIG(I,3)
	    PRF(4)=SIG(I,4)
	    CALL ELLIPS(PRF,STASIG,STAAXS,STATHE)
C
C CALCULATE THE FWHM SEEING DISK, ASSUMING A GAUSSIAN PROFILE
C AND PRINT THE STAR PARAMETERS
C
	    SEEING=STASIG*2.35482
	    ANGLE=STATHE*57.29578
	    WRITE(PRBUF,13)IDCHR,X(I),Y(I),SEEING,STAAXS
   13	    FORMAT(' ',A20,T23,2(SS,G13.6,1X),1X,SS,G10.3,6X,SS,G10.3)
	    CALL WRUSER(PRBUF,ISTAT)
	    WRITE(PRBUF,303) STATHE*57.29578
  303	    FORMAT(66X,SS,G10.3)
	    CALL WRUSER(PRBUF,ISTAT)
	  ELSE
C
C IF THE MARGINAL PROFILE WIDTHS WERE NOT FOUND, PRINT DETAILS
C
	    WRITE(PRBUF,14)IDCHR,X(I),Y(I)
   14	    FORMAT(' ',A20,T23,2(SS,G13.6,1X),3X,
     +	    'CANNOT FIT THIS STAR')
	    CALL WRUSER(PRBUF,ISTAT)
	  ENDIF
  101 	CONTINUE
      ENDIF
C
C NOW PRINT THE NUMBER OF STARS FOUND OK
C
      IF(ILEVEL.GE.2) THEN
	CALL WRUSER(' ',ISTAT)
	WRITE(PRBUF,15)NGOOD
   15	FORMAT(3X,I10,' STAR(S) FITTED SUCCESSFULLY')
	CALL LBGONE(PRBUF(4:))
	CALL WRUSER(PRBUF,ISTAT)
	CALL WRUSER(' ',ISTAT)
C
C PRINT THE MEAN RESULTS
C
	WRITE(PRBUF,16)AXISR
   16	FORMAT('   MEAN AXIS RATIO=',SS,G11.4)
	CALL WRUSER(PRBUF,ISTAT)
	CALL WRUSER(' ',ISTAT)
	WRITE(PRBUF,17)THETA*57.29578
   17   FORMAT('   MEAN INCLINATION OF MAJOR AXIS=',SS,G11.4,
     +	' DEGREES')
	CALL LBGONE(PRBUF(43:))
	CALL WRUSER(PRBUF,ISTAT)
	CALL WRUSER(' ',ISTAT)
      ENDIF
C
C CALL RADPRF TO DETERMINE THE FORM OF THE RADIAL PROFILE
C
      CALL RADPRF(IA,NPIX,NLINES,INVAL,ILEVEL,SIG0,AXISR,THETA,RANGE,
     +X,Y,NXY,SIG(1,5),IDEV,FWHM,GAMMA,IERRF)
C
C IF THE RADIAL PROFILE COULD NOT BE FOUND, SET IERR=2 AND ABORT
C
      IF(IERRF.NE.0) IERR=2
   99 RETURN
      END
