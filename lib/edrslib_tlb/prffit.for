      SUBROUTINE PRFFIT(P,PW,PR,NP,NITER,TOLL,AMP,BACK,SIGMA,
     +			GAMMA,IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO FIT A LEAST-SQUARES RADIAL STAR PROFILE TO APPROPRIATELY
*	BINNED DATA
*
*METHOD
*	THE ROUTINE REFINES INITIAL ESTIMATES OF THE STAR AMPLITUDE,
*	THE BACKGROUND, THE 'SIGMA' AND THE RADIAL EXPONENT G WHICH
*	ARE GIVEN ON ENTRY. THE ROUTINE REPEATEDLY FORMS AND SOLVES
*	THE LINEARISED NORMAL EQUATIONS FOR A LEAST-SQUARES FIT.
*
*ARGUMENTS
*	P (IN)
*	REAL(NP)
*		AN ARRAY OF DATA VALUES AT VARYING DISTANCES FROM THE
*		STAR CENTRE
*	PW (IN)
*	REAL(NP)
*		AN ARRAY OF WEIGHTS (INVERSELY PROPORTIONAL TO THE
*		SQUARE OF THE PROBABLE ERROR) ASSOCIATED WITH THE DATA
*		VALUES IN P
*	PR (IN)
*	REAL(NP)
*		AN ARRAY OF RADII ASSOCIATED WITH THE DATA VALUES IN P
*	NP (IN)
*	INTEGER
*		THE DIMENSION OF P,PW,PR
*	NITER (IN)
*	INTEGER
*		THE MAXIMUM NUMBER OF REFINING ITERATIONS
*	TOLL (IN)
*	REAL
*		THE FRACTIONAL ACCURACY REQUIRED IN THE RESULT
*		ACCURACY IS ASSESSED AS A FRACTION OF THE PARAMETER
*		VALUE FOR AMP,SIGMA AND GAMMA. THE ACCURACY OF BACK
*		IS ASSESSED AS A FRACTION OF AMP. ITERATIONS CEASE
*		WHEN TWO SUCCESSIVE ITERATIONS GIVE RESULTS AGREEING
*		WITHIN THE RELATIVE ACCURACY TOLL.
*	AMP (IN/OUT)
*	REAL
*		THE STAR AMPLITUDE
*	BACK (IN/OUT)
*	REAL
*		THE BACKGROUND SIGNAL
*	SIGMA (IN/OUT)
*	REAL
*		THE STAR 'SIGMA'
*	GAMMA (IN/OUT)
*	REAL
*		THE EXPONENT IN THE RADIAL PROFILE FUNCTION
*		THE FITTED FUNCTION IS:
*		    AMP*EXP(-0.5*(RADIUS/SIGMA)**GAMMA)+BACK
*	IERR (OUT)
*	INTEGER
*		ERROR FLAG: ZERO FOR SUCCESS
*			    1: ACCURACY CRITERION NOT MET
*
*CALLS
*	NAG LIBRARY:
*		F04ATF
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      PARAMETER (NPAR=4)
      REAL P(NP),PW(NP),PR(NP),X(NPAR)
      DOUBLE PRECISION R(NPAR),A(NPAR,NPAR),C(NPAR),DX(NPAR),
     +		       AA(NPAR,NPAR),WKS1(NPAR),WKS2(NPAR)
C
C COPY INITIAL PARAMETER ESTIMATES TO ARRAY X
C
      IERR=1
      X(1)=BACK
      X(2)=AMP
      X(3)=SIGMA
      X(4)=GAMMA
C
C BEGIN THE ITERATION LOOP:
C
      DO 88 ITER=1,NITER
C
C INITIALLISE ARRAYS FOR FORMING THE LINEARISED NORMAL EQUATIONS
C
	DO 6 I=1,NPAR
	  R(I)=0.0D0
	  DO 16 J=1,NPAR
	    A(I,J)=0.0D0
   16	  CONTINUE
    6   CONTINUE
C
C CONSIDER EACH DATA POINT WITH POSITIVE WEIGHT
C
        RSIG=1.0/X(3)
	DO 44 I=1,NP
	  IF(PW(I).GT.1.0E-10) THEN
C
C FORM THE REQUIRED PARAMETERS FOR THE NORMAL EQUATIONS
C
	    ALPHA=(PR(I)*RSIG)**X(4)
	    EX=EXP(-0.5*ALPHA)
	    BETA=X(2)*EX*ALPHA*0.5
	    C(1)=1.0D0
	    C(2)=EX
	    C(3)=BETA*RSIG*X(4)
	    C(4)=-BETA*LOG(MAX(1.0E-20,PR(I))*RSIG)
C
C FORM DEVIATION OF CURRENT PROFILE FROM DATA POINT
C
	    DELTA=(X(1)+X(2)*EX-P(I))*PW(I)
C
C FORM SUMS FOR NORMAL EQUATIONS
C
	    DO 174 J=1,NPAR
	      R(J)=R(J)+DELTA*C(J)
	      DO 173 K=1,J
		A(K,J)=A(K,J)+C(K)*C(J)*PW(I)
  173	      CONTINUE
  174	    CONTINUE
	  ENDIF
   44   CONTINUE
C
C FORM OTHER HALF OF SYMMETRIC MATRIX OF COEFFICIENTS
C
	DO 164 J=1,NPAR-1
	  DO 163 I=J+1,NPAR
	    A(I,J)=A(J,I)
  163	  CONTINUE
  164	CONTINUE
C
C CALL NAG ROUTINE F04ATF TO SOLVE THE NORMAL EQUATIONS
C
	IFAIL=1
	CALL F04ATF(A,NPAR,R,NPAR,DX,AA,NPAR,WKS1,WKS2,IFAIL)
C
C IF THE EQUATIONS WERE SINGULAR, ABORT WITH IERR=2
C
	IF(IFAIL.NE.0) THEN
	  IERR=2
	  GO TO 99
        ENDIF
C
C APPLY THE RESULTANT CORRECTIONS TO THE PROFILE PARAMETERS
C WITH DAMPING FACTORS FOR LARGE AMPLITUDE CHANGES
C
	X(1)=X(1)-DX(1)
	X(2)=X(2)-DX(2)/(1.0+2.0*ABS(DX(2)/MAX(1.0E-20,X(2))))
	X(3)=X(3)-DX(3)/(1.0+2.0*ABS(DX(3)/MAX(1.0E-20,X(3))))
	X(4)=X(4)-DX(4)/(1.0+2.0*ABS(DX(4)/MAX(1.0E-20,X(4))))
C
C FORM RELATIVE CHANGES IN EACH PARAMETER
C
	E1=ABS(DX(1)/MAX(1.0E-20,X(2)))
	E2=ABS(DX(2)/MAX(1.0E-20,X(2)))
	E3=ABS(DX(3)/MAX(1.0E-20,X(3)))
	E4=ABS(DX(4)/MAX(1.0E-20,X(4)))
C
C IF RELATIVE ACCURACY CRITERION IS MET, EXIT FROM ITERATION LOOP
C
	IF(MAX(E1,E2,E3,E4).LE.TOLL) THEN
	  BACK=X(1)
	  AMP=X(2)
	  SIGMA=X(3)
	  GAMMA=X(4)
	  IERR=0
	  GO TO 99
	ENDIF
   88 CONTINUE
   99 RETURN
      END
