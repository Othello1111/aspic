      SUBROUTINE GAUFIS(DATA,IMIN,IMAX,NITER,TOLL,AMP,X0,SIGMA,IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO FIT A GAUSSIAN WITH A SPECIFIED WIDTH TO A 1D ARRAY OF DATA
*
*METHOD
*	MAKE AN INITIAL ESTIMATE OF THE CENTRE AND AMPLITUDE BY
*	CENTROIDING FOLLOWED BY LINEAR LEAST SQUARES. THEN REFINE
*	THE ESTIMATES BY REPEATEDLY SOLVING THE LINEARISED NORMAL
*	EQUATIONS FOR THE CENTRE AND AMPLITUDE.
*
*ARGUMENTS
*	DATA (IN)
*	REAL(IMIN:IMAX)
*		THE INPUT DATA ARRAY
*	IMIN,IMAX (IN)
*	INTEGER
*		THE COORDINATES OF THE FIRST AND LAST DATA ELEMENTS
*		THESE ALSO DEFINE THE DIMENSION OF 'DATA'
*	NITER (IN)
*	INTEGER
*		MAXIMUM NUMBER OF REFINING ITERATIONS
*	TOLL (IN)
*	REAL
*		ACCURACY CRITERION: ABSOLUTE FOR THE CENTRE AND
*		RELATIVE FOR THE AMPLITUDE
*	AMP (OUT)
*	REAL
*		GAUSSIAN AMPLITUDE FOUND BY ROUTINE
*	X0 (OUT)
*	REAL
*		CENTRE FOUND BY ROUTINE
*	SIGMA (IN)
*	REAL
*		SIGMA FOR THE GAUSSIAN TO BE FITTED
*	IERR (OUT)
*	INTEGER
*		ERROR INDICATOR: ZERO FOR SUCCESS
*
*CALLS
*	NONE
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      REAL DATA(IMIN:IMAX),X(2),DX(2),C(2)
      DOUBLE PRECISION A(2,2),R(2)
C
C IERR=1 UNTIL THE PROCESS CONVERGES
C
      IERR=1
      POINTS=IMAX-IMIN+1
C
C FORM AN INITIAL ESTIMATE OF THE CENTRE AS THE DATA CENTROID
C
      SUM1=0.0
      SUM2=0.0
      DO 1 I=IMIN,IMAX
C
C DO NOT ALLOW NEGATIVE DATA
C
	D=MAX(DATA(I),0.0)
	SUM1=SUM1+D
	SUM2=SUM2+D*I
    1 CONTINUE
C
C IF THERE IS NO POSITIVE DATA, SET IERR=2 AND ABORT
C
      IF(SUM1.LE.0.0) THEN
	IERR=2
	GO TO 99
      ENDIF
      X(2)=SUM2/SUM1
C
C INITIALLISE SUMS FOR ESTIMATING THE AMPLITUDE
C
      SUM1=0.0
      SUM2=0.0
      RSIG=1.0/SIGMA
C
C ESTIMATE THE AMPLITUDE USING THE PREVIOUS CENTRE BY LINEAR 
C LEAST SQUARES
C
      DO 2 I=IMIN,IMAX
	DEV=(I-X(2))*RSIG
	DEV2=DEV*DEV
 	IF(DEV2.LE.25.0) THEN
C
C IGNORE POINTS MORE THAN 5 SIGMA FROM THE CENTRE
C
	  EX=EXP(-0.5*DEV2)
	  D=MAX(DATA(I),0.0)
	  SUM1=SUM1+EX*EX
	  SUM2=SUM2+D*EX
	ENDIF
    2 CONTINUE
C
C IF SIGMA IS VERY SMALL THERE MAY BE NO POINTS WITHIN 5 SIGMA
C ...ABORT WITH IERR=3
C
      IF(SUM1.LE.0.0) THEN
	IERR=3
	GO TO 99
      ENDIF
      X(1)=SUM2/SUM1
C
C NOW START THE MAIN ITERATION LOOP
C ---------------------------------
C
      RVAR=0.5/(SIGMA*SIGMA)
      RVAR2=2.0*RVAR
      DEVMAX=(5.0*SIGMA)**2
      DO 88 ITER=1,NITER
C
C INITIALLISE ARRAYS FOR LINEARISED NORMAL EQUATIONS
C
	R(1)=0.0D0
	R(2)=0.0D0
	A(1,1)=0.0D0
	A(2,1)=0.0D0
	A(1,2)=0.0D0
	A(2,2)=0.0D0
C
C FORM SUMS OVER THE DATA POINTS
C
	DO 44 I=IMIN,IMAX
	  DEV=I-X(2)
	  DEV2=DEV*DEV
C
C IGNORE POINTS BEYOND 5 SIGMA FROM THE CENTRE
C
	  IF(DEV2.LE.DEVMAX) THEN
	    EX=EXP(-RVAR*DEV2)
C
C SET UP SUMS FOR LINEARISED NORMAL EQUATIONS
C
	    C(1)=EX
	    C(2)=EX*X(1)*DEV*RVAR2
	    DELTA=X(1)*EX-DATA(I)
	    R(1)=R(1)+DELTA*C(1)
	    R(2)=R(2)+DELTA*C(2)
	    A(1,1)=A(1,1)+C(1)*C(1)
	    A(2,1)=A(2,1)+C(2)*C(1)
	    A(2,2)=A(2,2)+C(2)*C(2)
	  ENDIF
   44	CONTINUE
	A(1,2)=A(2,1)
C
C IF THE NORMAL EQUATIONS ARE SINGULAR, ABORT WITH IERR=4
C
	DET=A(1,1)*A(2,2)-A(1,2)*A(2,1)
	IF(DET.EQ.0.0) THEN
	  IERR=4
	  GO TO 99
	ENDIF
C
C SOLVE NORMAL EQUATIONS
C
	DX(1)=(R(1)*A(2,2)-A(1,2)*R(2))/DET
	DX(2)=(A(1,1)*R(2)-R(1)*A(2,1))/DET
C
C APPLY CORRECTIONS TO AMPLITUDE AND CENTRE WITH A DAMPING FACTOR
C FOR LARGE CHANGES
C
	X(1)=X(1)-DX(1)/(1.0+2.0*ABS(DX(1)/MAX(X(1),1.0E-10)))
	X(2)=X(2)-DX(2)/(1.0+2.0*ABS(DX(2)/POINTS))
C
C IF THE CONVERGENCE CRITERIA ARE SATISFIED, EXIT FROM ITERATION
C LOOP
C
	IF(ABS(DX(1)).LE.TOLL*X(1).AND.ABS(DX(2)).LE.TOLL) THEN
	  IERR=0
	  GO TO 501
	ENDIF
   88 CONTINUE
  501 AMP=X(1)
      X0=X(2)
   99 RETURN
      END
