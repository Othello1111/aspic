      SUBROUTINE ITFPLO
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO PLOT THE LINEARITY CORRECTION IN AN ITF TABLE
*
*METHOD
*	OBTAIN THE INPUT TABLE AND CHECK ITS VALIDITY. PLOT THE 
*	CONTENTS.
*
*ARGUMENTS
*	NONE
*
*STARLINK PARAMETERS
*	INPUT
*		INPUT ITF TABLE
*	ITFINVAL/ERROR/
*		ACCESSED IF THE ITF TABLE IS INVALID
*	NOSPACE/ERROR/
*		ACCESSED IF WORKSPACE IS NOT AVAILABLE
*	DEVICE
*		SPECIFIES GRAPHICS DEVICE TO BE USED
*
*CALLS
*	THIS PACKAGE:
*		GT2DIR,GTDSCR,TABPLT
*	STARLINK:
*		CNPAR,WRERR,FRDATA
*
*NOTES
*	USES VAX %VAL FACILITY
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER TITLE*30,CVAL*1,DEVICE*10
C
C OBTAIN INPUT ITF TABLE
C
   67 CALL GT2DIR('INPUT',204,.FALSE.,NENTRY,NLINET,IPTAB,IERRTB)
      IF(IERRTB.EQ.0) THEN
C
C SUCCESSFULLY OBTAINED... EXTRACT TITLE AND LOWER AND UPPER TABLE
C LIMITS FROM DESCRIPTOR
C
	TITLE=' '
	CALL GTDSCR('INPUT','TITLE','CHARACTER',IVAL,RVAL,TITLE,IERRT)
	BOTLIM=0.0
	CALL GTDSCR('INPUT','LOLIM','REAL',IVAL,BOTLIM,CVAL,IERRL)
	TOPLIM=0.0
        CALL GTDSCR('INPUT','UPLIM','REAL',IVAL,TOPLIM,CVAL,IERRU)
C
C CHECK VALIDITY OF INPUT... ALL DESCRIPTOR ITEMS PRESENT,
C ONLY 1 LINE IN THE IMAGE AND TOPLIM.GE.BOTLIM
C
        IF((IERRL.NE.0).OR.(IERRU.NE.0).OR.
     +  (NLINET.NE.1).OR.(BOTLIM.GT.TOPLIM)) THEN
C
C IF INVALID, GIVE ERROR MESSAGE AND RETURN TO GET NEW INPUT
C
	  CALL CNPAR('INPUT',ISTAT)
	  CALL WRERR('ITFINVAL')
	  GO TO 67
	ENDIF
C
C OBTAIN WORKSPACE FOR ROUTINE TABPLT
C
        CALL GETDYN('WORK',204,NENTRY,IPWRK,ISTWRK)
C
C IF WORKSPACE WAS NOT AVAILABLE, GIVE MESSAGE AND ABORT
C
	IF(ISTWRK.NE.0) THEN
	  CALL WRERR('NOSPACE')
	  GO TO 99
	ENDIF
C
C DETERMINE GRAPHICS DEVICE TO BE USED
C
	IDEV=2
	CALL GETCMD('DEVICE','ARGS,TEKTRONIX,GOC.',
     +	1,IDEV,DEVICE,LDEV,IERR)
C
C CALL TABPLT TO PLOT TABLE
C
	CALL TABPLT(%VAL(IPTAB),NENTRY,BOTLIM,TOPLIM,TITLE,
     +  IDEV,%VAL(IPWRK))
C
C FREE DATA AREA AND RETURN
C
      ENDIF
   99 CALL FRDATA(' ',ISTAT)
      RETURN
      END
