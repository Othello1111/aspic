      SUBROUTINE RNGERR(NAME,TYPE,BOTLIM,TOPLIM)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO PRINT MESSAGES SHOWING THE CORRECT DATA RANGE FOR A PARAMETER
*	AFTER THE USER HAS ENTERED AN INVALID VALUE
*
*METHOD
*	THE ROUTINE DISTINGUISHES BETWEEN INTEGER AND REAL PARAMETERS
*	AND ALLOWED AND EXCLUDED DATA RANGES, AND PRINTS AN APPROPRIATE
*	MESSAGE
*
*ARGUMENTS
*	NAME (IN)
*	CHARACTER*(*)
*		A NAME FOR THE PARAMETER, TO BE USED IN THE MESSAGE
*	TYPE (IN)
*	CHARACTER*(*)
*		'REAL' OR 'INTEGER', SPECIFIES THE PARAMETER TYPE
*	BOTLIM,TOPLIM (IN)
*	REAL
*		THE DATA LIMITS. IF BOTLIM.LE.TOPLIM, IT IS AN ALLOWED
*		RANGE. OTHERWISE IT IS AN EXCLUDED RANGE.
*
*CALLS
*	THIS PACKAGE:
*		LBGONE
*	STARLINK:
*		WRUSER
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER NAME*(*),TYPE*(*),ERRMSG*80
C
C IF PARAMETER IS A REAL VALUE
C ----------------------------
C
      IF(TYPE.EQ.'REAL')THEN
C
C IF LIMITS DEFINE A PERMITTED RANGE OF VALUES
C
	IF(TOPLIM.GE.BOTLIM) THEN
	  WRITE(ERRMSG,11)BOTLIM,TOPLIM
   11     FORMAT(' SHOULD LIE IN THE RANGE ',SS,
     +    G14.4,' TO ',SS,G14.4)
	  CALL LBGONE(ERRMSG(44:))
          CALL LBGONE(ERRMSG(37:))
          CALL LBGONE(ERRMSG(26:))
	ELSE
C
C IF LIMITS DEFINE AN EXCLUDED RANGE OF VALUES
C
	  WRITE(ERRMSG,12)TOPLIM,BOTLIM
   12     FORMAT(' SHOULD NOT LIE IN THE RANGE ',
     +    SS,G14.4,' TO ',SS,G14.4)
          CALL LBGONE(ERRMSG(48:))
          CALL LBGONE(ERRMSG(41:))
          CALL LBGONE(ERRMSG(30:))
	ENDIF
      ELSE
C
C IF THE PARAMETER IS AN INTEGER VALUE
C ------------------------------------
C
C IF THE LIMITS DEFINE A PERMITTED RANGE OF VALUES
C
	IF(TOPLIM.GE.BOTLIM) THEN
	  WRITE(ERRMSG,13)NINT(BOTLIM),NINT(TOPLIM)
   13     FORMAT(' SHOULD LIE IN THE RANGE ',SS,I14,
     +    ' TO ',SS,I14)
          CALL LBGONE(ERRMSG(44:))
          CALL LBGONE(ERRMSG(26:))
	ELSE
C
C IF LIMITS DEFINE AN EXCLUDED RANGE OF VALUES
C
	  WRITE(ERRMSG,14)NINT(TOPLIM),NINT(BOTLIM)
   14     FORMAT(' SHOULD NOT LIE IN THE RANGE ',SS,
     +    I14,' TO ',SS,I14)
          CALL LBGONE(ERRMSG(48:))
          CALL LBGONE(ERRMSG(30:))
	ENDIF
      ENDIF
C
C PRINT THE ERROR MESSAGE PRODUCED
C
      CALL WRUSER(' '//NAME//ERRMSG,ISTAT)
      RETURN
      END
