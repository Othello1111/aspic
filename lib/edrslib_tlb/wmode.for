      SUBROUTINE WMODE(X,W,NX,PBAD,NITER,TOLL,XMODE,SIGMA)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO FIND THE MOST LIKELY MEAN VALUE FOR A SET OF DATA POINTS
*	WITH DIFFERENT NORMALLY DISTRIBUTED ERRORS AND A CONSTANT
*	PROBABILITY OF BEING CORRUPT.
*
*METHOD
*	THIS ROUTINE USES THE STATISTICAL MODEL USED BY MODE TO
*	MINIMISE THE EFFECT OF CORRUPT DATA, BUT INCLUDES DIFFERENT
*	WEIGHTS FOR EACH DATA POINT, TO ALLOW FOR DIFFERENT INTRINSIC
*	ERRORS. SEE MODE FOR A FULLER DESCRIPTION OF THE ALGORITHM.
*
*ARGUMENTS
*	X (IN)
*	REAL(NX)
*		AN ARRAY OF DATA VALUES
*	W (IN)
*	REAL(NX)
*		AN ARRAY OF WEIGHTS FOR EACH DATA POINT IN X. THE
*		WEIGHTS ARE INVERSELY PROPORTIONAL TO THE SQUARE OF
*		THE RELATIVE ERRORS ON EACH DATA POINT.
*	NX (IN)
*	INTEGER
*		THE NUMBER OF DATA POINTS
*	PBAD (IN)
*	REAL
*		AN ESTIMATE OF THE PROBABILITY THAT ANY ONE DATA POINT
*		IS CORRUPT. (THIS VALUE NOT VERY CRITICAL)
*	NITER (IN)
*	INTEGER
*		THE MAXIMUM NUMBER OF ITERATIONS TO BE PERFORMED
*	TOLL (IN)
*	REAL
*		THE ABSOLUTE ACCURACY REQUIRED IN THE MEAN VALUE. 
*		ITERATIONS CEASE WHEN TWO SUCCESSIVE ITERATIONS AGREE
*		TO WITHIN THIS VALUE.
*	XMODE (OUT)
*	REAL
*		THE ESTIMATE OF THE UNCORRUPTED MEAN VALUE
*	SIGMA (OUT)
*	REAL
*		AN ESTIMATE OF THE UNCORRUPTED NORMALISED STANDARD
*		DEVIATION OF THE DATA POINTS. AN ESTIMATE OF THE
*		STANDARD DEVIATION OF ANY ONE DATA POINT IS:
*		    SIGMA/SQRT(W) , WHERE W IS ITS WEIGHT.
*
*CALLS
*	NONE
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      REAL X(NX),W(NX)
      DOUBLE PRECISION SUM1,SUM2,SUM3
C
C FORM SUMS FOR FINDING THE MEAN AND STANDARD DEVIATION OF THE 
C DATA
C
      SUM1=0.0D0
      SUM2=0.0D0
      SUM3=0.0D0
      DO 1 I=1,NX
	DATA=W(I)
	SUM1=SUM1+DATA
	DATA=DATA*X(I)
	SUM2=SUM2+DATA
	DATA=DATA*X(I)
	SUM3=SUM3+DATA
    1 CONTINUE
C
C CALCULATE THE WEIGHTED MEAN AND VARIANCE
C
      XMODE=SUM2/SUM1
      SIG2=(SUM3-(SUM2*SUM2)/SUM1)/NX
      SIG2=MAX(0.0,SIG2)
C
C HAVING FORMED THE INITIAL ESTIMATES, THE MAIN ITERATION LOOP
C STARTS HERE
C
      DO 3 ITER=1,NITER
C
C CALCULATE THE STANDARD DEVIATION AND NORMALISATION CONSTANTS
C
	SIG2=MAX(1.0E-20,SIG2)
	SIG1=SQRT(SIG2)
	W1=1.0/SIG1
	W2=0.5/SIG2
        PBNORM=0.707107*PBAD
C
C SET LIMIT AT WHICH POINTS ARE NEGLIGIBLE AT 10 STAND. DEVS.
C
	DEVLIM=100.0*SIG2
C
C INITIALLISE SUMS FOR FINDING NEW ESTIMATES
C
	SUMA=0.0
	SUMB=0.0
	SUMC=0.0
        SUMD=0.0
C
C COUNT THROUGH DATA POINTS
C
	DO 2 I=1,NX
C
C FIND DEVIATION FROM MODE, NORMALISED TO THAT EXPECTED FROM
C THE WEIGHTS SUPPLIED
C
	  DX=X(I)-XMODE
          DX2=DX*DX
          DEV2=DX2*W(I)
C
C IF POINT IS NOT NEGLIGIBLE, CALCULATE THE FRACTIONAL PROBABILITY 
C THAT IT IS GOOD
C
	  IF(DEV2.LE.DEVLIM) THEN
            EX=EXP(-W2*DEV2)
	    PROB=EX/(EX+PBNORM)
	    SUMA=SUMA+PROB
	    SUMB=SUMB+PROB*W(I)
	    SUMC=SUMC+DX*PROB*W(I)
	    SUMD=SUMD+DEV2*PROB
	  ENDIF
    2   CONTINUE
C
C CALCULATE THE NEW ESTIMATES
C
	SUMA=MAX(SUMA,1.0E-20)
	SUMB=MAX(SUMB,1.0E-20)
	DXMODE=SUMC/SUMB
	XMODE=XMODE+DXMODE
	SIG2=SUMD/SUMA
C
C IF REQUIRED ACCURACY HAS BEEN ACHIEVED, EXIT ITERATION LOOP
C
        IF(ABS(DXMODE).LE.TOLL) THEN
	  GO TO 7
	ENDIF
    3 CONTINUE
    7 SIGMA=SQRT(SIG2)
      RETURN
      END
