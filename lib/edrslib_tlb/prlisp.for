      SUBROUTINE PRLISP(LIST,NITEM,LSTLEN)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO PRINT THE CONTENTS OF AN X,Y LIST DATASET
*	ON A TERMINAL AND TO  A FILE
*
*METHOD
*	PRINT TITLE AND HEADINGS, PRINT TABLE OF IDENTIFIERS, X,Y 
*	POSITIONS AND MAGNITUDES IF PRESENT. PRINT TOTAL NUMBER OF
*	ENTRIES IN LIST.
*
*ARGUMENTS
*	LIST (IN)
*	INTEGER(NITEM,LSTLEN)
*		THE INPUT X,Y LIST
*	NITEM,LSTLEN (IN)
*	INTEGER
*		DIMENSIONS OF LIST
*
*CALLS
*	STARLINK:
*		WRUSER
*
*NOTES
*	USES BYTE ARRAYS
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*	Modified by KFH at RGO on 31-1-83
*-----------------------------------------------------------------------
C
C
C
C SET MAX NUMBER OF ITEMS TO BE PRINTED
C
      PARAMETER (MAXITM=8)
      INTEGER*4 LIST(NITEM,LSTLEN),BUF(MAXITM)
      BYTE ASC(20)
      REAL INFO(MAXITM-5)
C
C SET EQUIVALENCE SO THAT FIRST 20 BYTES ARE ACCESSED AS BYTE
C DATA AND SUBSEQUENT DATA IS REAL*4
C
      EQUIVALENCE (BUF(1),ASC(1)),(BUF(6),INFO(1))
      CHARACTER ID*20,TITLE(MAXITM-5)*14,ULINE(MAXITM-5)*14,PRBUF*80
      DATA TITLE/' X COORD.     ',' Y COORD.     ',' MAGNITUDE    '/
      DATA ULINE/' --------     ',' --------     ',' ---------    '/
C
C NINFO IS THE NUMBER OF NON-CHARACTER ITEMS TO BE PRINTED
C
      NINFO=MIN(MAXITM,NITEM)-5
C
C PRINT TITLES AND UNDERLINING
C
      WRITE(PRBUF,10)(TITLE(L),L=1,NINFO)
   10 FORMAT('       IDENTIFIER',T27,20A14)
      CALL WRUSER(PRBUF,ISTAT)
      WRITE (1,900) PRBUF
      WRITE(PRBUF,11)(ULINE(L),L=1,NINFO)
   11 FORMAT('       ----------',T27,20A14)
      CALL WRUSER(PRBUF,ISTAT)
      WRITE (1,900) PRBUF
  900 FORMAT(1H ,A80)
C
C SCAN THROUGH LIST ENTRIES
C
      DO 98 J=1,LSTLEN
C
C COPY ENTRY INTO BUFFER TO ALLOW DIFFERENT DATA TYPES
C
	DO 97 I=1,MIN(NITEM,MAXITM)
	  BUF(I)=LIST(I,J)
   97   CONTINUE
C
C CONVERT IDENTIFIER BYTES TO CHARACTERS
C
        DO 96 I=1,20
	  NCHAR=ASC(I)
	  ID(I:I)=CHAR(NCHAR)
   96	CONTINUE
C
C PRINT A LINE OF INFORMATION
C
	WRITE(PRBUF,12)ID,(INFO(L),L=1,NINFO)
   12   FORMAT(' ',A20,T27,20(SS,G13.6,:,1X))
	CALL WRUSER(PRBUF,ISTAT)
        WRITE (1,900) PRBUF
   98 CONTINUE
C
C FINALLY PRINT THE NUMBER OF ENTRIES
C
      CALL WRUSER(' ',ISTAT)
      IF(LSTLEN.NE.1) THEN
	WRITE(PRBUF,13)LSTLEN
   13   FORMAT(T22,I7,' LIST ENTRIES')
        CALL WRUSER(PRBUF,ISTAT)
        WRITE (1,910) PRBUF
  910   FORMAT (1H ,//,A80)
      ELSE
	CALL WRUSER('                            1 LIST ENTRY',ISTAT)
        WRITE (1,920)
  920   FORMAT (1H ,//,'			1 LIST ENTRY')
      ENDIF
      RETURN
      END
