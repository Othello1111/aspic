      SUBROUTINE MODE(X,NX,PBAD,NITER,TOLL,XMODE,SIGMA)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO ESTIMATE THE MEAN OF A NUMBER OF NORMALLY DISTRIBUTED DATA
*	VALUES, SOME OF WHICH MAY BE CORRUPT.
*
*METHOD
*	THE ROUTINE IS BASED ON MAXIMISING THE LIKELIHOOD FUNCTION
*	FOR A STATISTICAL MODEL IN WHICH ANY OF THE DATA POINTS HAS
*	A CONSTANT PROBABILITY OF BEING CORRUPT. A WEIGHTED MEAN IS
*	FORMED WITH WEIGHTS CHOSEN ACCORDING TO THE DEVIATION OF EACH
*	DATA POINT FROM THE CURRENT ESTIMATE OF THE MEAN. THE WEIGHTS
*	ARE DERIVED FROM THE RELATIVE PROBABILITIES OF BEING VALID
*	OR CORRUPT. A SEQUENCE OF THESE ITERATIONS CONVERGES TO A
*	STATIONARY POINT IN THE LIKELIHOOD FUNCTION. THE ROUTINE
*	APPROXIMATES TO A K-SIGMA CLIPPING ALGORITHM FOR A LARGE NUMBER
*	OF DATA POINTS AND TO A MODE-ESTIMATING ALGORITHM FOR FEWER
*	DATA POINTS.
*
*ARGUMENTS
*	X (IN)
*	REAL(NX)
*		AN ARRAY OF DATA VALUES
*	NX (IN)
*	INTEGER
*		THE NUMBER OF DATA VALUES
*	PBAD (IN)
*	REAL
*		AN ESTIMATE OF THE PROBABILITY THAT ANY ONE DATA POINT
*		WILL BE CORRUPT (THIS VALUE IS NOT CRITICAL)
*	NITER (IN)
*	INTEGER
*		THE MAXIMUM NUMBER OF ITERATIONS REQUIRED
*	TOLL (IN)
*	REAL
*		THE ABSOLUTE ACCURACY REQUIRED IN THE ESTIMATE OF THE
*		MEAN. ITERATIONS CEASE WHEN TWO SUCCESSIVE ESTIMATES
*		DIFFER BY LESS THAN THIS AMOUNT
*	XMODE (OUT)
*	REAL
*		THE ESTIMATE OF THE UNCORRUPTED MEAN
*	SIGMA (OUT)
*	REAL
*		AN ESTIMATE OF THE UNCORRUPTED STANDARD DEVIATION
*		OF THE DATA POINTS
*
*CALLS
*	NONE
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      REAL X(NX)
      DOUBLE PRECISION SUM1,SUM2,SUM3
C
C FORM INITIAL ESTIMATE OF MEAN AND SIGMA
C
      SUM1=0.0D0
      SUM2=0.0D0
      SUM3=0.0D0
      DO 1 I=1,NX
	SUM1=SUM1+1.0D0
	SUM2=SUM2+X(I)
	SUM3=SUM3+X(I)*X(I)
    1 CONTINUE
      XMODE=SUM2/SUM1
      SIG2=(SUM3-(SUM2*SUM2)/SUM1)/SUM1
      SIG2=MAX(SIG2,0.0)
C
C NOW START THE ITERATION LOOP
C
      PBNORM=PBAD*0.707107
      DO 3 ITER=1,NITER
	SIG2=MAX(SIG2,1.0E-20,1.0E-12*XMODE**2)
	W2=0.5/SIG2
C
C INITIALLISE SUMS FOR FORMING NEW ESTIMATE
C
	SUMA=0.0
	SUMB=0.0
	SUMC=0.0
	DEVLIM=100.0*SIG2
C
C SCAN THROUGH THE DATA POINTS, FORMING WEIGHTED SUMS TO CALCULATE
C NEW MEAN AND VARIANCE
C
	DO 2 I=1,NX
	  DEV=X(I)-XMODE
	  DEV2=DEV*DEV
C
C IGNORE POINTS MORE THAN 10 SIGMA FROM MODE
C
	  IF(DEV2.LE.DEVLIM) THEN
C
C WEIGHTS DEPEND ON THE FRACTIONAL PROBABILITY OF BEING GOOD DATA
C
            EX=EXP(-W2*DEV2)
            PROB=EX/(PBNORM+EX)
	    SUMA=SUMA+PROB
	    SUMB=SUMB+DEV*PROB
	    SUMC=SUMC+DEV2*PROB
	  ENDIF
    2	CONTINUE
C
C FORM THE NEW ESTIMATES
C
        SUMA=MAX(SUMA,1.0E-20)
	DXMODE=SUMB/SUMA
	XMODE=XMODE+DXMODE
	SIG2=SUMC/SUMA
C
C IF THE REQUIRED ACCURACY HAS BEEN MET, RETURN
C
	IF(ABS(DXMODE).LE.TOLL) GO TO 7
    3 CONTINUE
    7 SIGMA=SQRT(SIG2)
      END
