      SUBROUTINE ITFCOR
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO APPLY AN ITF TABLE LINEARITY CORRECTION TO AN IMAGE
*
*METHOD
*	OBTAIN INPUT IMAGE AND ITF TABLE. EXTRACT ITF TABLE DESCRIPTOR
*	ITEMS AND CHECK VALIDITY. OBTAIN OUTPUT IMAGE. EXTRACT IMAGE
*	DESCRIPTOR ITEMS, THEN CALL LNCORN TO APPLY THE ITF CORRECTION.
*	UPDATE THE OUTPUT DESCRIPTOR.
*
*ARGUMENTS
*	NONE
*
*STARLINK PARAMETERS
*	ILEVEL
*		INTERACTION LEVEL: CONTROLS PRINTING OF ITF TITLE
*	INPUT
*		INPUT IMAGE
*	ITFTABLE
*		INPUT ITF TABLE
*	ITFINVAL/ERROR/
*		ACCESSED IF THE ITF TABLE IS NOT VALID
*	OUTPUT
*		OUTPUT IMAGE
*	TITLE
*		TITLE TO REPLACE INPUT TITLE IN OUTPUT IMAGE
*
*CALLS
*	THIS PACKAGE:
*		GETPAR,GT2DIR,GTDSCR,GT2DIW,LNCORN,PTDSCR
*	STARLINK:
*		CNPAR,WRERR,WRUSER,CYDSCR,RDKEYC,FRDATA
*
*NOTES
*	USES VAX %VAL FACILITY
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER TITLE(1)*30,ITFTTL*30,CVAL*1
C
C OBTAIN INTERACTION LEVEL
C
      ILEVEL=2
      CALL GETPAR('ILEVEL','INTEGER',1,1.0,3.0,.TRUE.,ILEVEL,RVAL,IERR)
C
C OBTAIN INPUT IMAGE
C
      CALL GT2DIR('INPUT',102,.FALSE.,NPIX,NLINES,IPIN,IERR1)
      IF(IERR1.EQ.0) THEN
C
C SUCCESSFULLY OBTAINED... OBTAIN THE ITF TABLE TO BE APPLIED
C
   67	CALL GT2DIR('ITFTABLE',204,.FALSE.,NENTRY,NLINET,IPTAB,IERR2)
	IF(IERR2.EQ.0) THEN
C
C ITF TABLE SUCCESSFULLY OBTAINED... EXTRACT TITLE AND TABLE LIMITS
C FROM DESCRIPTOR
C
          ITFTTL=' '
	  CALL GTDSCR('ITFTABLE','TITLE','CHARACTER',IVAL,RVAL,ITFTTL,
     +	  IERRT)
	  BOTLIM=0.0
	  CALL GTDSCR('ITFTABLE','LOLIM','REAL',IVAL,BOTLIM,CVAL,IERRL)
	  TOPLIM=0.0
	  CALL GTDSCR('ITFTABLE','UPLIM','REAL',IVAL,TOPLIM,CVAL,IERRU)
C
C CHECK VALIDITY OF ITF TABLE... ALL DESCRIPTOR ITEMS PRESENT,
C ONLY 1 LINE IN THE INPUT IMAGE AND TOPLIM.GE.BOTLIM
C
      	  IF((IERRL.NE.0).OR.(IERRU.NE.0).OR.
     +    (NLINET.NE.1).OR.(BOTLIM.GT.TOPLIM)) THEN
C
C IF INVALID, GIVE ERROR MESSAGE AND RETURN TO GET NEW ITF TABLE
C
	    CALL CNPAR('ITFTABLE',ISTAT)
	    CALL WRERR('ITFINVAL')
	    GO TO 67
	  ENDIF
C
C SHOW THE USER THE ITF TABLE TITLE AND OBTAIN THE OUTPUT IMAGE
C
	  IF(ILEVEL.GE.2) THEN
	    CALL WRUSER(' ',ISTAT)
	    CALL WRUSER('   ITF TITLE= '//ITFTTL,ISTAT)
	    CALL WRUSER(' ',ISTAT)
	  ENDIF
	  CALL GT2DIW('OUTPUT',102,.FALSE.,NPIX,NLINES,IPOUT,IERR3)
	  IF(IERR3.EQ.0) THEN
C
C OUTPUT IMAGE OBTAINED... OBTAIN INVALID FLAG, SCALE AND ZERO FROM
C INPUT IMAGE
C
            TITLE(1)=' '
	    INVAL=-100000
	    ASCALE=1.0
	    AZERO=0.0
            CALL GTDSCR('INPUT','TITLE','CHARACTER',IVAL,RVAL,TITLE(1),
     +      IERR)
	    CALL GTDSCR('INPUT','INVAL','INTEGER',INVAL,RVAL,CVAL,IERR)
	    CALL GTDSCR('INPUT','BSCALE','REAL',IVAL,ASCALE,CVAL,IERR)
	    CALL GTDSCR('INPUT','BZERO','REAL',IVAL,AZERO,CVAL,IERR)
C
C SET OUTPUT INVALID PIXEL FLAG
C
	    IF(ABS(INVAL).LE.32767) THEN
	      INVALB=INVAL
	    ELSE
	      INVALB=-32767
	    ENDIF
C
C CALL LNCORN TO APPLY THE LINEARITY CORRECTION IN THE ITF TABLE
C
	    CALL LNCORN(%VAL(IPIN),NPIX,NLINES,INVAL,ASCALE,AZERO,
     +	    BOTLIM,TOPLIM,%VAL(IPTAB),NENTRY,%VAL(IPOUT),INVALB,
     +	    BSCALE,BZERO,IERR)
C
C COPY THE INPUT DESCRIPTOR TO THE OUTPUT AND UPDATE THE INVALID FLAG
C SCALE FACTOR AND ZERO
C
	    CALL CYDSCR('INPUT','OUTPUT',ISTAT)
	    CALL PTDSCR('OUTPUT','INVAL','INTEGER',INVALB,RVAL,CVAL,
     +      IERR)
	    CALL PTDSCR('OUTPUT','BSCALE','REAL',IVAL,BSCALE,CVAL,IERR)
	    CALL PTDSCR('OUTPUT','BZERO','REAL',IVAL,BZERO,CVAL,IERR)
C
C ADD TITLE
C
	    CALL RDKEYC('TITLE',.TRUE.,1,TITLE,NVAL,ISTAT)
            CALL PTDSCR('OUTPUT','TITLE','CHARACTER',IVAL,RVAL,TITLE(1),
     +	    IERR)
	  ENDIF
	ENDIF
      ENDIF
C
C RELEASE THE DATA AREAS AND RETURN
C
      CALL FRDATA(' ',ISTAT)
      RETURN
      END
