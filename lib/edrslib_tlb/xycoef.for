      SUBROUTINE XYCOEF
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO CALCULATE A SET OF 6 LINEAR TRANSFORMATION COEFFICIENTS
*	WHICH CORRESPOND TO A SPECIFIED SHIFT, ROTATION AND 
*	MAGNIFICATION
*
*METHOD
*	OBTAIN INPUT PARAMETERS FOR TRANSFORMATION, CALCULATE 
*	COEFFICIENTS AND WRITE TO THE ENVIRONMENT
*
*ARGUMENTS
*	NONE
*
*STARLINK PARAMETERS
*	ILEVEL
*		INTERACTION LEVEL: CONTROLS PRINTING OF RESULTS
*	CENTRE
*		CENTRE FOR ROTATION AND MAGNIFICATION
*	MAGNIFY
*		MAGNIFICATION FACTOR
*	ROTATION
*		ROTATION IN DEGREES (X THROUGH Y POSITIVE)
*	NEWCEN
*		NEW LOCATION OF CENTRE OF MAGNIFICATION AND ROTATION
*	TRCOEFFS
*		OUTPUT PARAMETER GIVING THE TRANSFORMATION COEFFICIENTS
*
*CALLS
*	THIS PACKAGE:
*		GETPAR,TRCOUT
*	STARLINK:
*		RDKEYR,WRUSER
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER CVAL*1,PRBUF*80
      REAL C(6),SHIFT(2),CENTRE(2)
C
C OBTAIN INTERACTION LEVEL
C
      ILEVEL=2
      CALL GETPAR('ILEVEL','INTEGER',1,1.0,3.0,.TRUE.,INVAL,RVAL,IERR)
C
C OBTAIN CENTRE OF ROTATION AND MAGNIFICATION
C
      CENTRE(1)=0.0
      CENTRE(2)=0.0
      CALL RDKEYR('CENTRE',.TRUE.,2,CENTRE,NVAL,ISTAT)
C
C OBTAIN MAGNIFICATION FACTOR
C
      RMAG=1.0
      CALL GETPAR('MAGNIFY','REAL',1,0.0,1.0E20,.TRUE.,IVAL,RMAG,IERR)
C
C OBTAIN ROTATION IN DEGREES, THEN CONVERT TO RADIANS
C
      ROT=0.0
      CALL GETPAR('ROTATION','REAL',1,-1.0E20,+1.0E20,.TRUE.,IVAL,ROT,
     +IERR)
      ROT=ROT*1.745329E-2
C
C OBTAIN NEW LOCATION OF CENTRE
C
      SHIFT(1)=CENTRE(1)
      SHIFT(2)=CENTRE(2)
      CALL RDKEYR('NEWCEN',.TRUE.,2,SHIFT,NVAL,ISTAT)
C
C CALCULATE THE TRANSFORM COEFFICIENTS
C
      CS=COS(ROT)
      SN=SIN(ROT)
      C(1)=SHIFT(1)-CENTRE(1)*RMAG*CS+CENTRE(2)*RMAG*SN
      C(2)=RMAG*CS
      C(3)=-RMAG*SN
      C(4)=SHIFT(2)-CENTRE(1)*RMAG*SN-CENTRE(2)*RMAG*CS
      C(5)=+RMAG*SN
      C(6)=RMAG*CS
C
C PRINT THE RESULTS
C
      IF(ILEVEL.GE.2) THEN
	CALL WRUSER(' ',ISTAT)
	CALL WRUSER('   TRANSFORMATION COEFFICIENTS:',ISTAT)
        CALL WRUSER(' ',ISTAT)
        DO 44 J=1,4,3
	  WRITE(PRBUF,19)((L,C(L)),L=J,J+2)
   19	  FORMAT(10X,3('C(',I1,')=',SS,G13.6,2X))
	  CALL WRUSER(PRBUF,ISTAT)
   44   CONTINUE
        CALL WRUSER(' ',ISTAT)
      ENDIF
C
C WRITE RESULTS TO THE ENVIRONMENT
C
      CALL TRCOUT('TRCOEFFS',C,6,ISTAT)
      END
