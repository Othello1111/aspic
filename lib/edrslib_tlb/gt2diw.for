      SUBROUTINE GT2DIW(NAME,IFMT,NULL,NPIX,NLINES,IPOINT,IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO OBTAIN AN OUTPUT 2 DIMENSIONAL IMAGE FRAME AND HANDLE ANY
*	ERRORS WHICH OCCUR
*
*METHOD
*	OBTAIN THE OUTPUT FRAME OF THE REQUIRED SIZE. IF ERRORS OCCUR
*	EXIT OR RETURN FOR A NEW FRAME AS APPROPRIATE
*
*ARGUMENTS
*	NAME (IN)
*	CHARACTER*(*)
*		THE PARAMETER NAME FOR THE OUTPUT FRAME
*	IFMT (IN)
*	INTEGER
*		THE STARLINK FORMAT CODE FOR THE OUTPUT FRAME
*	NULL (IN)
*	LOGICAL
*		IF TRUE, A NULL VALUE FOR THE FRAME IS OK. OTHERWISE
*		IT IS AN ERROR CONDITION
*	NPIX,NLINES (IN)
*	INTEGER
*		THE OUTPUT IMAGE DIMENSIONS
*	IPOINT (OUT)
*	INTEGER
*		THE POINTER TO THE DATA FRAME
*	IERR (OUT)
*	INTEGER
*		ERROR FLAG: ZERO FOR SUCCESS
*
*STARLINK PARAMETERS
*	'NAME'
*		THE PARAMETER NAME FOR THE OUTPUT FRAME IS GIVEN IN
*		THE ARGUMENT 'NAME'
*	NONULL/ERROR/
*		ACCESSED IF A NULL VALUE IS GIVEN FOR THE FRAME AND
*		'NULL' IS FALSE
*	NOACCESS/ERROR/
*		ACCESSED IF THE FRAME CANNOT BE ACCESSED
*	DIMOOR/ERROR/
*		ACCESSED IF THE IMAGE DIMENSIONS ARE OUT OF RANGE
*
*CALLS
*	STARLINK:
*		WRIMAG,WRERR,CNPAR
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      CHARACTER NAME*(*)
      LOGICAL NULL,EXIT
      INTEGER DIMS(2),CNSTAT
C
C SET MAX NUMBER OF ERRONEOUS NULL ENTRIES PERMITTED
C
      PARAMETER (MAXNUL=2)
C
C SET DIMENSIONS OF IMAGE
C
      DIMS(1)=NPIX
      DIMS(2)=NLINES
C
C INITIALLISE NULL ENTRY COUNTER AND CALL WRIMAG TO OBTAIN IMAGE FRAME
C
      NNULL=0
   11 IERR=0
      CALL WRIMAG(NAME,IFMT,DIMS,2,IPOINT,ISTAT)
C
C IF NULL WAS ENTERED:
C
      IF(ISTAT.EQ.1) THEN
	IF(.NOT.NULL) THEN
C
C IF NULL IS NOT PERMITTED:
C IF MAX NULL COUNT HAS NOT BEEN EXCEEDED, INCREMENT COUNT, GIVE
C MESSAGE AND GO BACK FOR A NEW ENTRY
C
	  IF(NNULL.LE.MAXNUL) THEN
	    NNULL=NNULL+1
	    CALL WRERR('NONULL')
	    CALL CNPAR(NAME,CNSTAT)
	    EXIT=.FALSE.
C
C IF MAX COUNT IS REACHED, ABORT WITH IERR=1
C
	  ELSE
	    IERR=1
	    EXIT=.TRUE.
	  ENDIF
C
C IF NULL IS OK, RETURN WITH IERR=1
C
	ELSE
	  IERR=1
	  EXIT=.TRUE.
	ENDIF
C
C IF FRAME CANNOT BE ACCESSED, GIVE MESSAGE AND RETURN FOR NEW FRAME
C
      ELSE IF(ISTAT.EQ.3) THEN
	CALL WRERR('NOACCESS')
	CALL CNPAR(NAME,CNSTAT)
	EXIT=.FALSE.
C
C IF PARAMETER NAME IS INVALID, RETURN WITH IERR=4
C
      ELSE IF(ISTAT.EQ.4) THEN
	IERR=4
	EXIT=.TRUE.
C
C IF FORMAT CODE IS INVALID, RETURN WITH IERR=5
C
      ELSE IF(ISTAT.EQ.5) THEN
	IERR=5
	EXIT=.TRUE.
C
C IF IMAGE DIMENSIONS ARE INVALID, GIVE MESSAGE AND RETURN WITH
C IERR=2
C
      ELSE IF(ISTAT.EQ.6) THEN
	CALL WRERR('DIMOOR')
	IERR=2
	EXIT=.TRUE.
C
C IF NONE OF THE ABOVE: IMAGE IS OK
C
      ELSE
	EXIT=.TRUE.
      ENDIF
C
C GO BACK FOR A NEW FRAME IF NECESSARY
C
      IF(.NOT.EXIT) GO TO 11
      RETURN
      END
