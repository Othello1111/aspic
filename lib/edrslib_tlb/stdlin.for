      SUBROUTINE STDLIN(ITYPE,BOTLIM,TOPLIM,C,MAXORD,TABLE,NTAB,IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO GENERATE LINEARITY CORRECTION TABLES USING STANDARD FORMULAE
*
*METHOD
*	EVALUATE THE FORMULAE AT REGULAR INTERVALS AND STORE IN A TABLE
*
*ARGUMENTS
*	ITYPE (IN)
*	INTEGER
*		THE TYPE OF LINEARITY CORRECTION REQUIRED
*			1: POLYNOMIAL
*			2: ELECTRONOGRAPHIC EMULSION SATURATION CORRN.
*	BOTLIM,TOPLIM (IN)
*	REAL
*		THE LOWER AND UPPER LIMITS FOR THE TABLE
*	C (IN)
*	REAL(MAXORD)
*		SET OF CONSTANTS FOR THE POLYNOMIAL FUNCTION
*		IF ITYPE=2, THE FIRST CONSTANT IS USED AS THE FILM 
*		CONSTANT
*	MAXORD (IN)
*	INTEGER
*		NUMBER OF CONSTANTS C
*	TABLE (OUT)
*	REAL(NTAB)
*		THE CORRECTION TABLE
*	NTAB (IN)
*	INTEGER
*		THE NUMBER OF ENTRIES IN THE TABLE
*	IERR (OUT)
*	INTEGER
*		ERROR FLAG: ZERO FOR SUCCESS
*
*CALLS
*	NONE
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      REAL TABLE(NTAB),C(MAXORD)
      IERR=0
C
C CHECK VALIDITY OF ARGUMENTS
C
      IF(BOTLIM.GT.TOPLIM) THEN
	IERR=1
      ELSE IF(NTAB.LT.2) THEN
	IERR=2
      ELSE
C
C CALCULATE INTERVAL BETWEEN TABLE ENTRIES
C
	DINTVL=(TOPLIM-BOTLIM)/(NTAB-1)
C
C SCAN THROUGH TABLE ENTRIES
C
	DO 1 IDEN=1,NTAB
	  D=BOTLIM+(IDEN-1)*DINTVL
C
C CALCULATE THE TABLE VALUE USING THE APPROPRIATE FORMULA
C
	  IF(ITYPE.EQ.1) THEN
	    DOUT=C(MAXORD)
	    DO 11 J=MAXORD-1,1,-1
	      DOUT=DOUT*D+C(J)
   11	    CONTINUE
C
	  ELSE IF(ITYPE.EQ.2) THEN
	    DOUT=-C(1)*LOG(1.0-D/C(1))
	  ENDIF
	  TABLE(IDEN)=DOUT
    1   CONTINUE
      ENDIF
      RETURN
      END
