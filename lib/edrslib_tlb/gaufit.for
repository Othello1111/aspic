      SUBROUTINE GAUFIT(DATA,IMIN,IMAX,NITER,TOLL,AMP,X0,SIGMA,
     +			BACK,IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO FIT A GAUSSIAN TO A 1 DIMENSIONAL ARRAY OF DATA
*
*METHOD
*	FIND INITIAL ESTIMATES OF THE GAUSSIAN AMPLITUDE, MEAN AND
*	WIDTH BY CENTROIDING AND LINEAR LEAST-SQUARES, USING THE
*	LOWER QUARTILE DATA POINT AS AN INITIAL BACKGROUND ESTIMATE.
*	REFINE THE ESTIMATES BY REPEATEDLY SOLVING THE LINEARISED NORMAL
*	EQUATIONS FOR A LEAST-SQUARES FIT.
*
*ARGUMENTS
*	DATA (IN)
*	REAL(IMIN:IMAX)
*		THE DATA ARRAY
*	IMIN,IMAX (IN)
*	INTEGER
*		THE COORDINATES OF THE FIRST AND LAST DATA ELEMENTS
*		THESE ALSO DEFINE THE DIMENSION OF 'DATA'
*	NITER (IN)
*	INTEGER
*		MAXIMUM NUMBER OF REFINING ITERATIONS
*	TOLL (IN)
*	REAL
*		ACCURACY CRITERION: ABSOLUTE FOR THE CENTRE AND
*		RELATIVE FOR THE AMPLITUDE AND WIDTH
*		ACCURACY OF BACK IS ASSESSED AS A FRACTION OF THE
*		GAUSSIAN AMPLITUDE
*	AMP (OUT)
*	REAL
*		THE GAUSSIAN AMPLITUDE
*	X0 (OUT)
*	REAL
*		THE GAUSSIAN CENTRE
*	SIGMA (OUT)
*	REAL
*		THE 'SIGMA' FOR THE GAUSSIAN
*	BACK (OUT)
*	REAL
*		THE BACKGROUND SIGNAL LEVEL
*	IERR (OUT)
*	INTEGER
*		ERROR INDICATOR: ZERO FOR SUCCESS
*		1: ACCURACY CRITERION NOT MET
*		2: ALL DATA HAS THE SAME VALUE
*		3: NORMAL EQUATIONS ARE SINGULAR
*
*CALLS
*	THIS PACKAGE:
*		NTHMIN
*	NAG LIBRARY:
*		F04ATF
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      REAL DATA(IMIN:IMAX),X(4),C(4),STAK(25)
      DOUBLE PRECISION SUM0,SUM1,SUM2,SUM3,A(4,4),R(4),T1,T2,T3,B,
     +		       DX(4),AA(4,4),WKS1(4),WKS2(4)
      PARAMETER (VARMIN=0.3**2)
C
C INITIALLISE SUMS FOR FORMING INITIAL ESTIMATE OF THE DATA MEAN
C AND STANDARD DEVIATION
C

      IERR=1
      POINTS=IMAX-IMIN+1
      SUM1=0.0D0
      SUM2=0.0D0
      SUM3=0.0D0
C
C FIND THE LOWER QUARTILE DATA POINT (OR THE 25'TH SMALLEST IF THERE
C ARE TOO MANY) TO USE AS AN INITIAL BACKGROUND ESTIMATE
C
      NQUART=MIN(MAX(1,NINT(POINTS*0.25)),25)
      CALL NTHMIN(DATA(IMIN),NINT(POINTS),NQUART,STAK,IERRQT)
      X(4)=STAK(1)
C
C FORM SUMS FOR MEAN AND STANDARD DEVIATION
C
      DO 1 I=IMIN,IMAX
C
C DO NOT COUNT NEGATIVE DATA
C
	D=MAX(0.0,DATA(I)-X(4))
	SUM1=SUM1+D
	D=D*I
	SUM2=SUM2+D
	D=D*I
	SUM3=SUM3+D
    1 CONTINUE
C
C IF THERE ARE NO POSITIVE DATA POINTS, ABORT WITH IERR=2
C
      IF(SUM1.LE.0.0D0) THEN
	IERR=2
	GO TO 99
      ENDIF
C
C FORM MEAN AND STANDARD DEVIATION
C
      X(2)=SUM2/SUM1
      X(3)=SQRT(MAX(DBLE(VARMIN),(SUM3-SUM2*X(2))/SUM1))
C
C INITIALLISE SUMS FOR FORMING AN INITIAL ESTIMATE OF THE GAUSSIAN
C AMPLITUDE
C
      SUM0=0.0D0
      SUM1=0.0D0
      SUM2=0.0D0
      SUM3=0.0D0
C
C CONSIDER THOSE DATA POINTS WITHIN 5 STANDARD DEVIATIONS OF THE MEAN
C
      RVAR=0.5/(X(3)**2)
      DEVMAX=(5.0*X(3))**2
      DO 2 I=IMIN,IMAX
	DEV=I-X(2)
	DEV2=DEV*DEV
	IF(DEV2.LE.DEVMAX) THEN
C
C FORM SUMS
C
	  EX=EXP(-RVAR*DEV2)
	  D=MAX(0.0,DATA(I)-X(4))
	  SUM0=SUM0+EX*EX
	  SUM1=SUM1+D*EX
	  SUM2=SUM2+D*DEV*EX
	  SUM3=SUM3+D*DEV2*EX
	ENDIF
    2 CONTINUE
C
C IF THERE WERE NO SATISFACTORY DATA POINTS, ABORT WITH IERR=2
C
      IF(SUM0.LE.0.0D0.OR.SUM1.LE.0.0D0) THEN
	IERR=2
	GO TO 99
      ENDIF
C
C FORM ESTIMATES OF THE GAUSSIAN HEIGHT AND MEAN
C
      X(1)=SUM1/SUM0
      X(2)=X(2)+SUM2/SUM1
C
C ESTIMATE THE VARIANCE, THEN MAKE AN APPROXIMATE CORRECTION
C FOR THE FACT THAT THE DATA POINTS WERE WEIGHTED WITH A GAUSSIAN
C USING THE OLD VALUE FOR THE VARIANCE
C
      VAR=(SUM3-(SUM2*SUM2)/SUM1)/SUM1
      OLDVAR=X(3)*X(3)
      VAR=(OLDVAR*VAR)/MAX(OLDVAR-VAR,1.0E-10)
      X(3)=SQRT(MAX(VARMIN,VAR))
C
C START THE ITERATION LOOP TO REFINE THE ESTIMATES OF THE GAUSSIAN
C PARAMETERS
C
      DO 88 ITER=1,NITER
C
C INITIALLISE ARRAYS FOR FORMING THE LINEARISED NORMAL EQUATIONS
C
	DO 6 I=1,4
	  R(I)=0.0D0
	  DO 16 J=1,4
	    A(I,J)=0.0D0
   16	  CONTINUE
    6	CONTINUE
C
C CONSIDER DATA POINTS WITHIN 5 STANDARD DEVIATIONS OF THE MEAN
C
	RVAR1=1.0/X(3)
	RVAR2=RVAR1*RVAR1
	RVAR=0.5*RVAR2
	DEVMAX=(5.0*X(3))**2
	DO 44 I=IMIN,IMAX
	  DEV=I-X(2)
	  DEV2=DEV*DEV
	  IF(DEV2.LE.DEVMAX) THEN
C
C FORM THE QUANTITIES REQUIRED IN THE NORMAL EQUATIONS
C
	    EX=EXP(-RVAR*DEV2)
	    C(1)=EX
	    C(2)=EX*X(1)*DEV*RVAR2
	    C(3)=C(2)*DEV*RVAR1
	    C(4)=1.0
C
C DELTA IS THE DEVIATION OF THE FIT FROM THE DATA
C
	    DELTA=X(1)*EX+X(4)-DATA(I)
C
C FORM SUMS FOR NORMAL EQUATIONS
C
	    R(1)=R(1)+DELTA*C(1)
	    R(2)=R(2)+DELTA*C(2)
	    R(3)=R(3)+DELTA*C(3)
	    R(4)=R(4)+DELTA*C(4)
	    A(1,1)=A(1,1)+C(1)*C(1)
	    A(2,1)=A(2,1)+C(2)*C(1)
	    A(3,1)=A(3,1)+C(3)*C(1)
	    A(4,1)=A(4,1)+C(4)*C(1)
	    A(2,2)=A(2,2)+C(2)*C(2)
	    A(3,2)=A(3,2)+C(3)*C(2)
            A(4,2)=A(4,2)+C(4)*C(2)
	    A(3,3)=A(3,3)+C(3)*C(3)
	    A(4,3)=A(4,3)+C(4)*C(3)
	    A(4,4)=A(4,4)+C(4)*C(4)
	  ENDIF
   44	CONTINUE
C
C FORM THE COMPLETE SYMMETRIC MATRIX OF NORMAL COEFFICIENTS
C
	A(1,2)=A(2,1)
	A(1,3)=A(3,1)
	A(2,3)=A(3,2)
	A(1,4)=A(4,1)
	A(2,4)=A(4,2)
	A(3,4)=A(4,3)
C
C CALL NAG ROUTINE F04ATF TO SOLVE THE LINEARISED NORMAL EQUATIONS
C
	IFAIL=1
	CALL F04ATF(A,4,R,4,DX,AA,4,WKS1,WKS2,IFAIL)
	IF(IFAIL.NE.0) THEN
	  IERR=3
	  GO TO 99
	ENDIF
C
C APPLY THE CORRECTIONS TO THE GAUSSIAN PARAMETERS WITH DAMPING
C FACTORS FOR LARGE AMPLITUDE CHANGES
C
	X(1)=X(1)-DX(1)/(1.0+2.0*ABS(DX(1)/MAX(X(1),1.0E-20)))
	X(2)=X(2)-DX(2)/(1.0+2.0*ABS(DX(2)/POINTS))
	X(3)=X(3)-DX(3)/(1.0+2.0*ABS(DX(3)/MAX(X(3),1.0E-20)))
	X(4)=X(4)-DX(4)/(1.0+2.0*ABS(DX(4)/MAX(X(1),1.0E-20)))
C
C IF THE ACCURACY CRITERION IS MET, EXIT FROM THE ITERATION LOOP
C
	IF((ABS(DX(1)).LE.TOLL*X(1)).AND.(ABS(DX(2)).LE.TOLL).AND.
     +	(ABS(DX(3)).LE.TOLL*X(3)).AND.(ABS(DX(4)).LE.TOLL*X(1))) THEN
	  IERR=0
	  AMP=X(1)
	  X0=X(2)
	  SIGMA=X(3)
	  BACK=X(4)
	  GO TO 99
        ENDIF
   88 CONTINUE
   99 RETURN
      END
