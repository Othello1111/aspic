      SUBROUTINE SETKNT(X,NX,XKNOT,NXKNOT)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO PLACE INTERIOR KNOTS SO AS TO MAINTAIN AN EVEN DISTRIBUTION
*	OF DATA POINTS BETWEEN THE KNOT POSITIONS WHEN FITTING SPLINE
*	FUNCTIONS TO DATA.
*
*METHOD
*	CALCULATE THE MEAN NUMBER OF DATA POINTS PER KNOT INTERVAL.
*	PLACE THE KNOTS THIS NUMBER OF DATA POINTS APART, INTERPOLATING
*	LINEARLY BETWEEN THE DATA POSITIONS TO OBTAIN THE KNOT POSITIONS
*
*ARGUMENTS
*	X (IN)
*	DOUBLE PRECISION(NX)
*		THE DATA POSITIONS
*	NX (IN)
*	INTEGER
*		THE NUMBER OF DATA POINTS
*	XKNOT (OUT)
*	DOUBLE PRECISION(NXKNOT)
*		THE INTERIOR KNOT POSITIONS
*	NXKNOT (IN)
*	INTEGER
*		THE REQUIRED NUMBER OF INTERIOR KNOTS
*
*CALLS
*	NONE
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      DOUBLE PRECISION X(NX),XKNOT(NXKNOT)
C
C FIND MEAN NUMBER OF DATA POINTS PER KNOT INTERVAL
C
      XINTVL=(NX-1)/(NXKNOT+1.0)
C
C COUNT THROUGH EACH KNOT, CALCULATING THE NO. OF DATA POINTS
C LESS THAN THE REQUIRED KNOT VALUE
C
      DO 1 I=1,NXKNOT
	DATAPT=1.0+XINTVL*I
C
C THE RESULT IS NOT USUALLY AN INTEGER, SO INTERPOLATE LINEARLY
C BETWEEN THE ORDERED DATA POSITIONS
C
	NDAT1=DATAPT
	NDAT2=MIN(NX,NDAT1+1)
	DDATA=DATAPT-NDAT1
	XKNOT(I)=X(NDAT1)*(1.0-DDATA)+X(NDAT2)*DDATA
    1 CONTINUE
      RETURN
      END
