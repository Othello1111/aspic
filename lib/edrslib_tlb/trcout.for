      SUBROUTINE TRCOUT(NAME,C,N,ISTAT)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO FIT SIX REAL NUMBERS INTO LESS THAN 63 CHARACTERS SO THAT
*	THEY MAY BE USED TOGETHER AS AN OUTPUT PROGRAM PARAMETER.
*	MAXIMUM PRECISION IS MAINTAINED.
*
*METHOD
*	WRITE EACH NUMBER TO A CHARACTER STRING. ADD THE EXPONENTIAL
*	IF REQUIRED, KEEPING UNNECESSARY CHARACTERS TO A MINIMUM. WRITE
*	TO ENVIRONMENT AS AN ARRAY OF CHARACTER VALUES.
*
*ARGUMENTS
*	NAME (IN)
*	CHARACTER*(*)
*		NAME OF PROGRAM PARAMETER FOR OUTPUT
*	C (IN)
*	REAL(N)
*		UP TO 6 NUMBERS TO BE OUTPUT
*	N (IN)
*	INTEGER
*		A NUMBER NOT MORE THAN 6, THE NUMBER OF OUTPUT VALUES
*	ISTAT (OUT)
*	INTEGER
*		STATUS RETURN FROM WRKEYC
*
*STARLINK PARAMETERS
*	'NAME'
*		THE PARAMETER SPECIFIED IN THE ARGUMENT 'NAME' IS
*		ASSIGNED A NEW VALUE BY THE ROUTINE
*
*CALLS
*	THIS PACKAGE:
*		LBGONE
*	STARLINK:
*		WRKEYC
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      REAL C(N)
      CHARACTER NAME*(*),BUF(6)*10,EBUF*9,BUFOUT(6)*9
C
C FORMAT UP TO 6 NUMBERS
C
      DO 99 I=1,MIN(N,6)
C
C FIND THE EXPONENT FROM THE LOG, ENSURING DOWNWARD ROUNDING
C
        IF(ABS(C(I)).GT.1.0E-20) THEN
          NEXP=INT(LOG10(ABS(C(I)))+100.0)-100
C
C CONVERT TO THE RANGE 1.000 TO 9.9999..
C
          D=C(I)/(10.0**NEXP)
        ELSE
          NEXP=0
          D=0.0
        ENDIF
C
C WRITE INTO BUFFER, THEN LEFT JUSTIFY IF NO SIGN PRESENT
C
        WRITE(BUF(I),1) D
    1   FORMAT(SS,F10.7)
        CALL LBGONE(BUF(I))
C
C IF THE EXPONENT IS NOT ZERO, WRITE TO A SEPARATE BUFFER
C
        IF(NEXP.NE.0) THEN
	  WRITE(EBUF,2) NEXP
    2	  FORMAT(SS,I9)
C
C COPY EXPONENT INTO OUTPUT BUFFER AT LEAST SIGNIFICANT END
C
          DO 4 J=9,1,-1
	    IF(EBUF(J:J).NE.' ') THEN
	      BUF(I)(J:J)=EBUF(J:J)
            ELSE
C
C ADD 'E' IN FRONT OF EXPONENT
C
	      BUF(I)(J:J)='E'
	      GO TO 5
	    ENDIF
    4	  CONTINUE
    5     CONTINUE
        ENDIF
        BUFOUT(I)=BUF(I)
   99 CONTINUE
C
C WRITE RESULTING CHARACTER ARRAY TO ENVIRONMENT
C
      CALL WRKEYC(NAME,BUFOUT,MIN(N,6),ISTAT)
      END
