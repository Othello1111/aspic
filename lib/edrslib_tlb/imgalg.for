      SUBROUTINE IMGALG(IA,ASCALE,AZERO,INVALA,IB,BSCALE,BZERO,INVALB,
     +			NPIX,NLINES,NFUNCT,CSCALE,CZERO,INVALC,IC)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO IMPLEMENT 4 FUNCTION ARITHMETIC BETWEEN 2 IMAGES OF THE
*	SAME SIZE
*
*METHOD
*	PERFORM REAL ARITHMETIC USING THE FUNCTION REQUIRED, THEN
*	CONVERT THE RESULT TO INTEGERS
*
*ARGUMENTS
*	IA (IN)
*	INTEGER*2(NPIX,NLINES)
*		THE FIRST INPUT IMAGE
*	ASCALE,AZERO (IN)
*	REAL
*		SCALE AND ZERO FOR IMAGE IA
*	INVALA (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IA
*	IB (IN)
*	INTEGER*2(NPIX,NLINES)
*		SECOND INPUT IMAGE
*	BSCALE,BZERO (IN)
*	REAL
*		SCALE AND ZERO FOR IB
*	INVALB (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IB
*	NPIX,NLINES (IN)
*	INTEGER
*		DIMENSIONS OF IMAGES
*	NFUNCT (IN)
*	INTEGER
*		SPECIFIES WHICH FUNCTION IS REQUIRED:+,-,* OR /.
*	CSCALE,CZERO (IN)
*	REAL
*		SCALE AND ZERO FOR OUTPUT IMAGE IC
*	INVALC (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IC
*	IC (OUT)
*	INTEGER*2(NPIX,NLINES)
*		OUTPUT IMAGE
*
*CALLS
*	NONE
*
*NOTES
*	USES INTEGER*2 ARRAYS
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
      INTEGER*2 IA(NPIX,NLINES),IB(NPIX,NLINES),IC(NPIX,NLINES)
      REAL MININT,MAXINT
C
C SET MINIMUM AND MAXIMUM PERMISSIBLE INTEGER VALUES
C
      PARAMETER (MININT=-32767.0,MAXINT=32767.0)
      RCSCAL=1.0/SIGN(MAX(ABS(CSCALE),1.0E-20),CSCALE)
C
C SCAN IMAGE
C
      DO 2 J=1,NLINES
	DO 1 I=1,NPIX
C
C IF BOTH INPUT PIXELS ARE VALID, CONTINUE WITH ARITHMETIC
C
	  IF((IA(I,J).NE.INVALA).AND.(IB(I,J).NE.INVALB)) THEN
C
C CALCULATE TRUE DATA VALUES
C
	    A=IA(I,J)*ASCALE+AZERO
	    B=IB(I,J)*BSCALE+BZERO
C
C SELECT AND PERFORM REQUIRED FUNCTION
C
	    GO TO (101,102,103,104) NFUNCT
  101	    C=((A+B)-CZERO)*RCSCAL
	    GO TO 200
  102	    C=((A-B)-CZERO)*RCSCAL
	    GO TO 200
  103	    C=((A*B)-CZERO)*RCSCAL
	    GO TO 200
  104	    IF(B.EQ.0.0) THEN
	      C=MAXINT+1.0
	    ELSE
	      C=((A/B)-CZERO)*RCSCAL
	    ENDIF
  200       CONTINUE
C
C IF ANSWER LIES IN THE VALID RANGE, ADD TO OUTPUT IMAGE
C
	    IF((C.LE.MAXINT).AND.(C.GE.MININT)) THEN
	      IC(I,J)=NINT(C)
	    ELSE
	      IC(I,J)=INVALC
	    ENDIF
C
C IF EITHER INPUT PIXEL WAS INVALID, SO IS THE OUTPUT PIXEL
C
	  ELSE
	    IC(I,J)=INVALC
	  ENDIF
    1   CONTINUE
    2 CONTINUE
      RETURN
      END
