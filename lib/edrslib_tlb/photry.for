      SUBROUTINE PHOTRY(IA,NPIX,NLINES,INVAL,SCALE,ZERO,X0,Y0,
     +			ISIDE,SIGMA,E,THETA,G,RANGE,X,Y,A,B,
     +			TOTI,TOTMAG,IERR)
*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
*PURPOSE
*	TO CALCULATE THE INTEGRATED INTENSITY OF A STAR
*
*METHOD
*	CALL LOCSTA TO LOCATE THE STAR CENTRE. PUT THE SURROUNDING
*	BINS INTO CIRCULAR OR ELLIPTICAL ISOPHOTAL ZONES, AND MAINTAIN
*	A LINKED LIST OF ALL THE VALID PIXELS IN EACH ZONE. IN EACH ZONE
*	CALL MODE TO FIND A MOST LIKELY DATA VALUE, AND FIT THE
*	SPECIFIED RADIAL STAR PROFILE TO THE RESULTING DATA POINTS USING
*	LINEAR LEAST SQUARES. FINALLY CALCULATE THE INTEGRATED INTENSITY
*	OF THE FITTED FUNCTION.
*
*ARGUMENTS
*	IA (IN)
*	INTEGER*2(NPIX,NLINES)
*		THE IMAGE
*	NPIX,NLINES (IN)
*	INTEGER
*		THE DIMENSIONS OF IA
*	INVAL (IN)
*	INTEGER
*		INVALID PIXEL FLAG FOR IA
*	SCALE,ZERO (IN)
*	REAL
*		SCALE AND ZERO LEVEL FOR IA
*	X0,Y0 (IN)
*	REAL
*		INITIAL APPROXIMATE STAR CENTRE
*	ISIDE (IN)
*	INTEGER
*		SIDE OF SQUARE WITHIN WHICH DATA IS USED
*	SIGMA (IN)
*	REAL
*		THE 'SIGMA' OF THE STAR PROFILE
*	E (IN)
*	REAL
*		THE AXIS RATIO OF THE ELLIPTICAL STAR IMAGE
*	THETA (IN)
*	REAL
*		THE INCLINATION OF THE MAJOR AXIS TO THE X DIRECTION
*		IN RADIANS (X THROUGH Y POSITIVE)
*	G (IN)
*	REAL
*		THE EXPONENT IN THE RADIAL STAR PROFILE
*	RANGE (IN)
*	REAL
*		THE RADIUS, IN UNITS OF THE STAR 'SIGMA', OUT TO 
*		WHICH THE RADIAL PROFILE IS FITTED
*	X,Y (OUT)
*	REAL
*		THE ACCURATE STAR CENTRE
*	A (OUT)
*	REAL
*		THE STAR CENTRAL AMPLITUDE
*	B (OUT)
*	REAL
*		THE BACKGROUND LEVEL
*	TOTI (OUT)
*	REAL
*		THE INTEGRATED INTENSITY OF THE STAR
*	TOTMAG (OUT)
*	REAL
*		THE MAGNITUDE (-2.5*LOG10(TOTI))
*	IERR (OUT)
*	INTEGER
*		ERROR FLAG: ZERO FOR SUCCESS
*
*CALLS
*	THIS PACKAGE:
*		LOCSTA,MODE
*	NAG LIBRARY:
*		S14AAF
*
*NOTES
*	USES INTEGER*2 ARRAYS
*
*WRITTEN BY
*	R.F. WARREN-SMITH
*-----------------------------------------------------------------------
C
C
C
C SET THE MAXIMUM RADIUS TO BE USED IN THE RADIAL PROFILE
C (MAXRAD) AND THE PROBABILITY OF CORRUPT DATA (PBAD)
C
      PARAMETER (MAXRAD=50,
     +	         PBAD=0.01,
     +		 MAXPTS=(2*MAXRAD+1)**2,
     +		 NITER=5,
     +		 TWOPI=6.283185)
C
C SET THE RESOLUTION AT WHICH THE IMAGE WILL BE BINNED INTO
C ISOPHOTAL ZONES (NO. OF BINS PER PIXEL SPACING)
C
      PARAMETER (RESOL=3.0,
     +		 MAXBIN=MAXRAD*RESOL+1.0)
C
C DIMENSION ARRAYS... NOTE IMAGE IS TREATED AS A 1D ARRAY
C
      INTEGER*2 IA(NPIX*NLINES)
      REAL DATA(MAXPTS),R(0:MAXBIN)
      INTEGER NPTS(0:MAXBIN),LSTART(0:MAXBIN),NXTADR(MAXPTS),
     +IADR(MAXPTS),BIN
      DOUBLE PRECISION SUMW,SUMF,SUMF2,SUMDF,SUMD,S14AAF
      IERR=0
C
C CALCULATE A MEAN SIGMA AND CALL LOCSTA TO LOCATE THE STELLAR
C IMAGE
C
      SIG=SIGMA*(1.0+0.5*E)
      CALL LOCSTA(IA,NPIX,NLINES,INVAL,X0,Y0,ISIDE,X,Y,SIG,IERR)
C
C IF THE STAR WAS FOUND, CONTINUE. OTHERWISE RETURN WITH THE
C VALUE OF IERR
C
      IF(IERR.EQ.0) THEN
C
C CALCULATE THE CONSTANTS RELATING RADIAL DISTANCE TO EFFECTIVE
C DISTANCE ALONG THE MINOR AXIS FOR AN ELLIPTICAL STAR IMAGE
C
	S=SIN(THETA)
	C=COS(THETA)
	RE2=(1.0/E)**2
	CONSTA=S*S+C*C*RE2
	CONSTB=C*C+S*S*RE2
	CONSTC=2.0*C*S*(RE2-1.0)
C
C CALCULATE THE SCALE FACTOR TO CONVERT RADIAL DISTANCE TO BINS
C
	RSIG=1.0/SIGMA
	RSCALE=RESOL*2.0/MIN(2.0,SIGMA)
C
C CALCULATE THE MAXIMUM EFFECTIVE RADIUS SET BY THE FITTING LIMIT
C 'RANGE'
C
	RLIMIT=RANGE*SIGMA
C
C CALCULATE THE SEARCH SQUARE SIZE REQUIRED TO ACCOMMODATE THE MAXIMUM
C RADIUS
C
	ISHIFT=MIN(MAX(1,NINT(RLIMIT*E)),MAXRAD)
C
C CALCULATE THE MAXIMUM NUMBER OF RADIAL BINS REQUIRED TO HOLD THE
C RADIAL PROFILE
C
	MXBINN=MIN(MAXBIN,INT(RLIMIT*RSCALE))
C
C CALCULATE THE EDGES OF THE SQUARE
C
	I0=NINT(X)
	J0=NINT(Y)
	IMIN=MAX(1,I0-ISHIFT)
	IMAX=MIN(NPIX,I0+ISHIFT)
	JMIN=MAX(1,J0-ISHIFT)
	JMAX=MIN(NLINES,J0+ISHIFT)
C
C INITIALLISE ARRAYS FOR STORING MEAN RADII OF ISOPHOTAL BINS
C AND FOR FORMING LINKED LISTS OF ALL THE DATA POINTS IN
C EACH ISOPHOTAL ZONE
C
	DO 37 BIN=0,MXBINN
	  R(BIN)=0.0
	  NPTS(BIN)=0
	  LSTART(BIN)=0
   37	CONTINUE
C
C SCAN THROUGH THE SQUARE AROUND THE STAR, CALCULATING THE
C X AND Y DISPLACEMENTS FROM THE CENTRE
C
	INDEX=0
	DY=JMIN-Y-1.0
	DX0=IMIN-X-1.0
	DO 97 J=JMIN,JMAX
	  IMLOCN=(J-1)*NPIX+IMIN-1
	  DY=DY+1.0
	  DY2=DY*DY
	  DX=DX0
	  DO 96 I=IMIN,IMAX
	    IMLOCN=IMLOCN+1
	    DX=DX+1.0
C
C IF THE PIXEL IS VALID, THEN CALCULATE THE EFFECTIVE RADIUS
C
	    IF(IA(IMLOCN).NE.INVAL) THEN
	      DX2=DX*DX
	      DXY=DX*DY
	      RDASH=SQRT(CONSTA*DX2+CONSTB*DY2+CONSTC*DXY)
C
C FIND THE ISOPHOTAL BIN FOR THIS PIXEL
C
	      BIN=RDASH*RSCALE
	      IF(BIN.LE.MXBINN.AND.RDASH.LE.RLIMIT) THEN
C
C FORM SUMS FOR THE MEAN EFFECTIVE RADIUS
C
	        NPTS(BIN)=NPTS(BIN)+1
		R(BIN)=R(BIN)+RDASH
C
C STORE A POINTER TO THE PIXEL IN A LINKED LIST OF ALL THE PIXELS
C IN THIS ISOPHOTAL ZONE
C
		INDEX=INDEX+1
		IADR(INDEX)=IMLOCN
		NXTADR(INDEX)=LSTART(BIN)
		LSTART(BIN)=INDEX
	      ENDIF
	    ENDIF
   96	  CONTINUE
   97   CONTINUE
C
C INITIALLISE SUMS FOR FORMING THE LEAST SQUARES FIT OF THE
C SPECIFIED RADIAL PROFILE OF THE STAR
C
	SUMW=0.0D0
	SUMF=0.0D0
	SUMF2=0.0D0
	SUMDF=0.0D0
	SUMD=0.0D0
C
C COUNT THROUGH EACH OF THE ISOPHOTAL BINS, CONSIDERING THOSE
C CONTAINING 1 OR MORE DATA POINTS
C
	DO 100 BIN=0,MXBINN
	  IF(NPTS(BIN).GE.1) THEN
C
C OBTAIN THE LOCATION OF THE START OF THE LINKED LIST OF VALUES
C IN THE BIN, THEN READ EACH VALUE AND STORE IN THE ARRAY 'DATA'
C
	    ICOUNT=0
	    INDEX=LSTART(BIN)
  144	    IF(INDEX.GT.0) THEN
	      ICOUNT=ICOUNT+1
	      DATA(ICOUNT)=IA(IADR(INDEX))
	      INDEX=NXTADR(INDEX)
	      GO TO 144
	    ENDIF
C
C CALL MODE TO FIND THE MOST LIKELY DATA VALUE WITHIN THE BIN
C
	    CALL MODE(DATA,NPTS(BIN),PBAD,NITER,0.1,DMODE,DSIG)
C
C FIND THE MEAN RADIUS FOR THE BIN AND CALCULATE THE STELLAR PROFILE
C
	    RADIUS=R(BIN)/NPTS(BIN)
	    FUNCT=EXP(-0.5*((RADIUS*RSIG)**G))
C
C NOW FORM THE SUMS FOR THE LEAST SQUARES FIT, USING THE NUMBER OF
C POINTS AS A WEIGHT
C
	    WT=NPTS(BIN)
	    FW=FUNCT*WT
	    SUMW=SUMW+WT
	    SUMF=SUMF+FW
	    SUMF2=SUMF2+(FUNCT*FW)
	    SUMDF=SUMDF+(DMODE*FW)
	    SUMD=SUMD+(DMODE*WT)
	  ENDIF
  100 	CONTINUE
C
C IF THE NORMAL EQUATIONS ARE SINGULAR, RETURN WITH IERR=4
C
	DET=SUMF2*SUMW-SUMF*SUMF
	IF(DET.EQ.0.0) THEN
	  IERR=4
	  GO TO 99
	ENDIF
C
C SOLVE THE NORMAL EQUATIONS FOR THE STAR CENTRAL DENSITY AND THE
C BACKGROUND, APPLYING THE SCALE AND ZERO FOR THE IMAGE
C
	A=(SUMW*SUMDF-SUMD*SUMF)/DET
	B=(SUMF2*SUMD-SUMDF*SUMF)/DET
	A=A*SCALE
	B=B*SCALE+ZERO
C
C CALCULATE THE INTEGRATED INTENSITY UNDER THE FITTED PROFILE
C (S14AAF EVALUATES THE GAMMA FUNCTION)
C
        IFAIL=1
	TOTI=((TWOPI*A*E/G)*((2.0*SIGMA**G)**(2.0/G)))*
     +  S14AAF(DBLE(2.0/G),IFAIL)
	IF(TOTI.GT.0.0) THEN
	  TOTMAG=-2.5*LOG10(TOTI)
	ELSE
	  TOTMAG=0.0
	  IERR=5
	ENDIF
      ENDIF
   99 RETURN
      END
