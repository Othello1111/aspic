C
C
C
      SUBROUTINE IAMEDRSS
C+
C     IAMEDRSS.
C
C     Program to convert a .BDF frame generated by the ASPIC IAM
C     suite into the XYlist format of the EDRS package.
C
C  Given;
C   None.
C
C  Returned;
C   None.
C
C  Subroutines called;
C   This program:-       OUTLIST, IAMKOPY, IAMDESCR.
C   Interfaces:-         INPICR, OUTPUT, CLEARIM.
C   Interim envirn.:-    FRDATA, CNPAR.
C
C  A C Davenhall./ROE/                                    5/10/83.
C-
      IMPLICIT NONE
C
      INTEGER IOSTAT
C
      INTEGER EDRSPTR,EDRSTAT,RECSIZE,RECORDS
C
      INTEGER IAMPTR,IAMSTAT,AXIS(2),IMAGES,PARAMS
C
      CHARACTER BUFFER*60
C
C    OFFSET  -  The offset (in words) of the data in every EDRS record.
C               This is needed because every record starts with a 
C               20 byte character identifier.
C    FIELDS  -  The number of numeric fields in every EDRS record.
C               In the present case there are 15 IAM parameters and
C               hence 15 fields.
C
      INTEGER OFFSET,FIELDS
      PARAMETER (OFFSET=5)
      PARAMETER (FIELDS=15)
C
C
      IOSTAT=0
C
C    Attempt to obtain a pointer to the input IAM frame.
C
      IAMSTAT=0
      CALL INPICR ('IAMFRAME',
     :      'Give the name of the input IAM file;',
     :       2,AXIS,IAMPTR,IAMSTAT)
      PARAMS=AXIS(1)
      IMAGES=AXIS(2)
C
C    Proceed if obtained Ok.
C
      IF (IAMSTAT.EQ.0) THEN
C
C    Attempt to obtain a pointer to the output XYlist.
C
        RECORDS=IMAGES
        RECSIZE=FIELDS+OFFSET
        EDRSTAT=0
        CALL OUTLIST ('EDRSLST',
     :      'Give the name of the EDRS output file;',
     :       RECSIZE,RECORDS,EDRSPTR,EDRSTAT)
C
C    Proceed if obtained Ok.
C
        IF (EDRSTAT.EQ.0) THEN
C
C    Copy the input IAM array into the output EDRS array.
C
          CALL IAMKOPY (OFFSET,FIELDS,IMAGES,PARAMS,%VAL(IAMPTR),
     :                  RECORDS,RECSIZE,%VAL(EDRSPTR))
C
C    Write titles for the fields as image descriptors.
C
          CALL IAMDESCR ('EDRSLST',EDRSTAT)
        END IF
C
        IF (EDRSTAT.NE.0) THEN
          CALL OUTPUT (
     :  '***ERROR whilst accessing EDRS XYlist file.',IOSTAT)
          WRITE(BUFFER,2000) EDRSTAT
 2000     FORMAT(10X,'Status raised = ',I5)
          CALL OUTPUT (BUFFER,IOSTAT)
        END IF
      ELSE
        CALL OUTPUT (
     :   '***ERROR whilst accessing IAM input file.',IOSTAT)
        WRITE(BUFFER,2000) IAMSTAT
        CALL OUTPUT (BUFFER,IOSTAT)
      END IF
C
C    Tidy up images.
C
      CALL CLEARIM ('IAMFRAME')
      CALL FRDATA  ('EDRSLST',EDRSTAT)
      CALL CNPAR   ('EDRSLST',EDRSTAT)
C
      END
