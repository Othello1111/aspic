C
C	LEAST SQUARES FIT TO A STRAIGHT LINE.
C
	SUBROUTINE LINLSQ (X,Y,NPTS,NARRAY,OUTPUT,GRAD,C,
	1		   SIGG,SIGC,STD,UNIT)
	DIMENSION X(NARRAY),Y(NARRAY)
	INTEGER UNIT
	LOGICAL OUTPUT
 2001	FORMAT(5(/),43X,'Least Squares Fit to a Straight Line.'///
	1 47X,'Gradient = ',1PE12.4,/47X,'Intercept = ',1PE12.4,
 	2 /47X,'Error on Gradient = ',1PE12.4/
	3 47X,'Error on Intercept = ',1PE12.4///)
	DATA ZERO,ONE,TWO/0.0E0,1.0E0,2.0E0/
	SXI=ZERO
	SYI=ZERO
	SXI2=ZERO
	SXIYI=ZERO
	DO 3 I=1,NPTS
	XX=X(I)
	YY=Y(I)
	SXI=SXI+XX
	SYI=SYI+YY
	SXI2=SXI2+(XX*XX)
 3	SXIYI=SXIYI+(XX*YY)
	DELTA=(NPTS*SXI2)-(SXI*SXI)
	GRAD=((NPTS*SXIYI)-(SXI*SYI))/DELTA
	C=((SXI2*SYI)-(SXI*SXIYI))/DELTA
	STD=ZERO
	DO I=1,NPTS
	   STD=STD+((Y(I)-C-(GRAD*X(I)))**2)
	END DO
	STD=STD/(NPTS-TWO)
	SIGG=SQRT(STD*NPTS/DELTA)
	SIGC=SQRT(STD*SXI2/DELTA)
	IF (OUTPUT) WRITE(UNIT,2001) GRAD,C,SIGG,SIGC
	RETURN
	END
