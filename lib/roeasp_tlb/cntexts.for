      SUBROUTINE CNTEXTS
C+
C     CNTEXTS.
C
C     Subroutine to extract a set of "contours" from a region
C     of an image. This routine is intended to be used
C     to generate input for an ellipse fitting routine. 
C     Thus the contours are generated by considering
C     all the points inside an intensity "band" rather
C     merely those at precisely the contour level. Thus
C     the routine is probably not suitable for
C     purely "contouring" applications.
C
C  Given;
C   None.
C
C  Returned;
C   None.
C
C  Subroutines called;
C   Interfaces:-  INPICR, OUTPICR, READI, READR, OUTPUT, CLEARIM.
C   E2D:-         BNDGEN, ELLEXT, CNKOPY.
C
C  Structure:-
C   Attempt to obtain pointer to input image
C   attempt to obtain pointer to work array.
C   if pointers obtained ok
C     Attempt to obtain the coords. of the region to be contoured
C     if read ok
C       prompt for base contour and increment
C       generate set of contour levels.
C       extract contours from specified region.
C       attempt to obtain pointers to output arrays.
C       if pointers obtained ok
C         copy work arrays to output arrays.
C       else
C         print error message.
C       end if
C     else
C       print message
C     end if
C   else
C     print message
C   end if
C   tidy up images.
C
C  A C Davenhall./ROE/                                   14/9/82.
C  A C Davenhall./ROE/   {Modified}                      25/9/82.
C-
      INTEGER XEXT,YEXT,IMPTR,IMSTAT,AXIS1(2)
C
      INTEGER MAXPAR,MAXLEV,MAXCNT,NLEVEL,WK1PTR,CNTPTR,AXIS2(3)
      PARAMETER (MAXPAR=2)
      PARAMETER (MAXPTS=5000)
       PARAMETER (MAXCNT=50)
      INTEGER LEVPT(MAXCNT)
      REAL INTEN(2,MAXCNT),LOGI(MAXCNT)
      INTEGER LGIPTR,INTPTR,AXIS3(1)
C
      INTEGER XBASE,YBASE,XTOP,YTOP,CORSTT,OUTSTT
      REAL MINLGI,DELOGI,HWIDTH
      INTEGER EXTSTT
      CHARACTER BUFFER*70
C
C
      IOSTAT=0
C
C    Attempt to obtain pointers to Starlink images;
C    ... input image containing the galaxy to be contoured.
C
      IMSTAT=0
      CALL INPICR ('IMAGE',
     : ' Enter file name for image to be analysed;',
     :   2,AXIS1,IMPTR,IMSTAT)
C
C    ... work array to hold the extracted contours.
C
      AXIS2(1)=MAXPAR
      AXIS2(2)=MAXPTS
      AXIS2(3)=MAXCNT
      CALL OUTPICR ('WORK',' Work array.',
     :   3,AXIS2,WK1PTR,IMSTAT)
C
C    Proceed if the pointers have been obtained Ok.
C
      IF (IMSTAT.EQ.0) THEN
        XEXT=AXIS1(1)
        YEXT=AXIS1(2)
C
C    Attempt to obtain coords. for the region to be contoured.
C
        CORSTT=0
        CALL READI ('XBASE',
     :   ' Enter lower X coord. of region to be contoured;',
     :     1,1,XEXT,XBASE,CORSTT)
        CALL READI ('YBASE',
     :   ' Enter lower Y coord. of required region;',
     :     XBASE,1,YEXT,YBASE,CORSTT)
        CALL READI ('XTOP',
     :   ' Enter upper X coord. of required region;',
     :     XEXT,XBASE,XEXT,XTOP,CORSTT)
        CALL READI ('YTOP',
     :   ' Enter upper Y coord. of required region;',
     :     XTOP,YBASE,YEXT,YTOP,CORSTT)
C
C    Proceed if these values have been obtained Ok.
C
        IF (CORSTT.EQ.0) THEN
C
C    Prompt for details of the required contours.
C
          CALL READR ('MINLGI',
     :     ' Enter lowest Log I value to be contoured;',
     :       -1.0E0,-5.0E0,1.0E1,MINLGI,IOSTAT)
          CALL READR ('DELOGI',
     :     ' Increment in Log I between the contours;',
     :       1.0E-1,1.0E-3,1.0E2,DELOGI,IOSTAT)
C
C    Set the half width of the bands to 1/5 of the
C    increment between bands.
C
          HWIDTH=DELOGI/5.0E0
C
          CALL OUTPUT ('Please wait. Contours being extracted now.',
     :                  IOSTAT)
C
C    Generate a set of isophotal bands.
C
          CALL BNDGEN (MAXCNT,MAXPAR,MINLGI,DELOGI,HWIDTH,
     :                 MAXCNT,LOGI,INTEN)
C
C    Extract the contours.
C
          NLEVEL=MAXCNT
          CALL ELLEXT (XEXT,YEXT,%VAL(IMPTR),XBASE,YBASE,XTOP,YTOP,
     :                 MAXCNT,MAXPTS,MAXPAR,NLEVEL,LOGI,INTEN,
     :                 %VAL(WK1PTR),LEVPT,EXTSTT)
          WRITE(BUFFER,2000) NLEVEL
 2000     FORMAT(1X,'No. of contours extracted = ',I3)
          CALL OUTPUT ('  ',IOSTAT)
          CALL OUTPUT (BUFFER,IOSTAT)
          CALL OUTPUT ('  ',IOSTAT)
          IF (EXTSTT.EQ.0) THEN
            CALL OUTPUT (' Contours extracted successfully.',IOSTAT)
          ELSE
            CALL OUTPUT (
     :       ' ***ERROR Insufficient space to hold all the points.',
     :             IOSTAT)
            CALL OUTPUT (
     :       '          Some contours will be incomplete.',IOSTAT)
          END IF
C
C    Attempt to obtain pointers to the output arrays.
C    ... Contour file.
C
          OUTSTT=0
          AXIS2(1)=MAXPAR
          AXIS2(2)=MAXPTS
          AXIS2(3)=NLEVEL
          CALL OUTPICR ('CONTOURS',
     :     ' Enter name of file for the extracted contours;',
     :       3,AXIS2,CNTPTR,OUTSTT)
C
C    ... Log I values.
C
          AXIS3(1)=NLEVEL
          CALL OUTPICR ('LOGI',
     :     ' Enter name of file to hold contour intensities;',
     :       1,AXIS3,LGIPTR,OUTSTT)
C
C    Proceed if the pointers obtained Ok.
C
          IF (OUTSTT.EQ.0) THEN
            CALL CNKOPY (MAXPAR,MAXPTS,MAXCNT,%VAL(WK1PTR),LOGI,
     :                   NLEVEL,%VAL(CNTPTR),%VAL(LGIPTR))
          ELSE
            CALL OUTPUT (
     :  ' ***ERROR Unable to obtain output files successfully.',
     :       IOSTAT)
          END IF
        ELSE
          CALL OUTPUT (
     :     ' ***ERROR Unable to obtain coords. of region to be ',
     :            IOSTAT)
          CALL OUTPUT (
     :     '          contoured properly. Such a region may be ',
     :            IOSTAT)
          CALL OUTPUT (
     :     '          defined using BOXASP.',IOSTAT)
        END IF
      ELSE
        CALL OUTPUT (
     :   ' ***ERROR Unable to obtain input image correctly.',IOSTAT)
      END IF
C
C    tidy up images.
C
      CALL CLEARIM ('IMAGE')
      CALL CLEARIM ('CONTOURS')
      CALL CLEARIM ('LOGI')
      CALL CLEARIM ('WORK')
      END
